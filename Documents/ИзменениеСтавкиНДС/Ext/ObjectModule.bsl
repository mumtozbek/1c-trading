
Процедура ЗаполнитьОстатки(Коллекция, Дата, Обновление = Ложь) Экспорт
	ТаблицаЗначений = Коллекция.Выгрузить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Остатки.Товар КАК Товар,
	|	ЦеныТоваров.Цена КАК ЦенаБазовая,
	|	ИсторияРеквизитовСтавкаНДС.Значение КАК СтавкаНДС
	|ИЗ
	|	РегистрНакопления.Остатки.Остатки(&Дата, Порожняя = Ложь И Бронь = Ложь) КАК Остатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныТоваров.СрезПоследних(&Дата, Тип = ""ЦенаБазовая"") КАК ЦеныТоваров ПО ЦеныТоваров.Товар.Ссылка = Остатки.Товар.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРеквизитов.СрезПоследних(&Дата, Реквизит = ""СтавкаНДС"") КАК ИсторияРеквизитовСтавкаНДС ПО ИсторияРеквизитовСтавкаНДС.Ссылка.Ссылка = Остатки.Товар.Ссылка
	|ГДЕ
	|	Остатки.КоличествоОстаток <> 0 И (Остатки.Товар.ВидТовара.ТипТовара = Значение(Перечисление.ТипыТовара.Продукция) ИЛИ Остатки.Товар.ВидТовара.ТипТовара = Значение(Перечисление.ТипыТовара.Товар))
	|УПОРЯДОЧИТЬ ПО
	|	Остатки.Товар.Наименование
	|ИТОГИ
	|	СУММА(ЦенаБазовая)
	|ПО
	|	Товар");

	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
	Пока ВыборкаТовар.Следующий() Цикл
		СтарыеСтроки = ТаблицаЗначений.НайтиСтроки(Новый Структура("Товар", ВыборкаТовар.Товар));
		Если СтарыеСтроки.Количество() > 0 Тогда
			ТекСтрока = СтарыеСтроки[0];
		Иначе
			ТекСтрока = ТаблицаЗначений.Добавить();
		КонецЕсли;

		ТекСтрока.Товар = ВыборкаТовар.Товар;
		ТекСтрока.ЦенаБазовая = ВыборкаТовар.ЦенаБазовая;
		ТекСтрока.СтавкаНДС = ?(ЗначениеЗаполнено(СтавкаНДС), СтавкаНДС, ВыборкаТовар.СтавкаНДС);
	КонецЦикла;
	
	Если (НЕ Обновление) Тогда
		ТаблицаЗначений.Сортировать("Товар");
	КонецЕсли;
	
	Коллекция.Загрузить(ТаблицаЗначений);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Подготовка регистров сведения
	Движения.ЦеныТоваров.Записывать = Истина;
	Движения.ИсторияРеквизитов.Записывать = Истина;
	
	Для Каждого Строка Из Товары Цикл
		// Цены товары - ЦенаБазовая
		Если Строка.ЦенаБазовая > 0 Тогда
			Движение = Движения.ЦеныТоваров.Добавить();
			Движение.Период = Дата;
			Движение.Товар = Строка.Товар;
			Движение.Тип = "ЦенаБазовая";
			Движение.Цена = Строка.ЦенаБазовая;
		КонецЕсли;
		
		Движение = Движения.ИсторияРеквизитов.Добавить();
		Движение.Период = Дата;
		Движение.Ссылка = Строка.Товар;
		Движение.Реквизит = "СтавкаНДС";
		Движение.Значение = Строка.СтавкаНДС;
	КонецЦикла;
КонецПроцедуры
