
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтгрузкаЗаказов") Тогда
		// Заполнение шапки
		Автор = ДанныеЗаполнения.Автор;
		Источник = ДанныеЗаполнения.Источник;
		Комментарий = ДанныеЗаполнения.Комментарий;
		Организация = ДанныеЗаполнения.Организация;
		Склад = ДанныеЗаполнения.Склад;
		Отгрузка = ДанныеЗаполнения.Ссылка;
		
		Для Каждого НакладнаяСтрока Из Отгрузка.Накладные Цикл
			Для Каждого ТоварСтрока Из НакладнаяСтрока.Накладная.Товары Цикл
				Если ТоварСтрока.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ТоварСтрока.Товар.Тара) Тогда
					Строка = Возвраты.Добавить();
					Строка.Контрагент = НакладнаяСтрока.Контрагент;
					Строка.Тара = ТоварСтрока.Товар.Тара;
					Строка.Отгружено = ТоварСтрока.Количество;
					Строка.Возвращено = ТоварСтрока.Количество;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Возвраты.Свернуть("Контрагент,Тара", "Отгружено,Возвращено");
	КонецЕсли;
	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// регистр Остатки
	Движения.Остатки.Записывать = Истина;
	
	// регистр Возвраты
	Движения.Возвраты.Записывать = Истина;
	
	// регистр СальдоПоТаре
	Движения.СальдоПоТаре.Записывать = Истина;

	Для Каждого Строка Из Возвраты Цикл
		// Приход / расход тары
		Движение = Движения.Остатки.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Товар = Строка.Тара;
		Движение.Порожняя = Истина;
		Движение.Примечание = глНайтиПримечания(Комментарий);
		Движение.Количество = Строка.Возвращено;
		
		// Приход / расход сальдо по таре
		Движение = Движения.СальдоПоТаре.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Товар = Строка.Тара;
		Движение.Контрагент = Строка.Контрагент;
		Движение.Примечание = глНайтиПримечания(Комментарий);
		Движение.Количество = Строка.Возвращено;
		
		// Возвраты доставщиков
	 	Движение = Движения.Возвраты.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Отгрузка.Водитель;
		Движение.Товар = Строка.Тара;
		Движение.Примечание = глНайтиПримечания(Комментарий);
		Движение.Количество = Строка.Возвращено;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ДокументПоОтгрузе = Документы.ВозвратТары.НайтиПоРеквизиту("Отгрузка", Отгрузка);
	Если ЗначениеЗаполнено(ДокументПоОтгрузе) И ДокументПоОтгрузе.Ссылка <> Ссылка Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Имеется возврат по выбранной отгрузке.";
		Сообщение.Поле = "Отгрузка";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Отказ = Истина;;
	КонецЕсли;
	
	Если Возвраты.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не заполнена таблица возвратов.";
		Сообщение.Поле = "Возвраты";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Отказ = Истина;;
	КонецЕсли;
КонецПроцедуры
