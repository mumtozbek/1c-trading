
// Предопределенные
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьИтоги();
КонецПроцедуры

// Шапка
&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
    Если Объект.Товары.Количество() > 0 И Вопрос("Хотите установить новую ставку для всех товаров?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		Для Каждого Строка Из Объект.Товары Цикл
			Строка.СтавкаНДС = Объект.СтавкаНДС;
			
			ТоварыПересчитатьСтроку(Строка.ПолучитьИдентификатор());
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Основные действия
&НаКлиенте
Процедура ОсновныеДействияФормыПриемныйАкт(Кнопка)
	ФактурыКлиент.СформироватьПриемныйАкт(Объект)
КонецПроцедуры

// Команды
&НаСервере
Процедура КомандаТоварыЗаполнитьНаСервере()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьТовары(Объект.Товары, Объект.Контрагент, Объект.Дата, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КомандаТоварыЗаполнить(Кнопка)
	КомандаТоварыЗаполнитьНаСервере();
	
	ОбновитьИтоги();
КонецПроцедуры

&НаСервере
Процедура КомандаТоварыОбновитьНаСервере()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьТовары(Объект.Товары, Объект.Контрагент, Объект.Дата, Истина);
	
	Для Каждого Строка Из Объект.Товары Цикл
		ТоварыПересчитатьСтроку(Строка.ПолучитьИдентификатор(), "ТоварыСтавкаНДС");
	КонецЦикла;
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомандаТоварыОбновить(Кнопка)
	КомандаТоварыОбновитьНаСервере();
	
	ОбновитьИтоги();
КонецПроцедуры

&НаКлиенте
Процедура КомандаТоварыУдалитьПС(Кнопка)
	ПустыеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Количество", 0)); 
	Для Каждого Строка Из ПустыеСтроки Цикл 
		Объект.Товары.Удалить(Строка); 
	КонецЦикла;
КонецПроцедуры

// Шапка
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	Объект.Договор = ОбщийМодульСервер.ПолучитьЗначениеРеквизита(Объект.Контрагент, "ОсновнойДоговор");
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеРасходыПриИзменении(Элемент)
	ОбновитьИтоги();
КонецПроцедуры

// Товары
&НаКлиенте
Процедура ТоварыТоварПриИзменении(Элемент)
	Строка = Элементы.Товары.ТекущиеДанные;
	Строка.ЦенаБазовая = РаботаСТоварами.Цена(Строка.Товар, "ЦенаБазовая", Объект.Дата, Строка.ЦенаБазовая);
	Строка.СтавкаНДС = ИсторияРеквизитов.Получить(Строка.Товар, "СтавкаНДС", Объект.Дата, ПредопределенноеЗначение("Справочник.СтавкиНДС.БезНДС"));
	Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаБазовая, ОбщийМодульСервер.ПолучитьЗначениеРеквизита(Строка.СтавкаНДС, "Ставка"));
	Строка.ЦенаОтпускная = РаботаСТоварами.Цена(Строка.Товар, "ЦенаПродажная", Объект.Дата, Строка.ЦенаОтпускная);
	
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБазоваяПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБазоваяПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПродажнаяПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПродажнаяПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаОтпускнаяПриИзменении(Элемент)
	ТоварыПересчитатьСтроку(Элементы.Товары.ТекущаяСтрока, Элементы.Товары.ТекущийЭлемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОбновитьИтоги();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	ОбновитьИтоги();
КонецПроцедуры

&НаСервере
Процедура ТоварыПересчитатьСтроку(ИдентификаторСтроки, Колонка = "ТоварыКоличество")
	Строка = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	// Количество
	Если Колонка = "ТоварыКоличество" Тогда
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
		Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
	КонецЕсли;
	
	// СтавкаНДС
	Если Колонка = "ТоварыСтавкаНДС" Тогда
		Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаБазовая, Строка.СтавкаНДС);
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
	КонецЕсли;
	
	// Цена
	Если Лев(Колонка, 10) = "ТоварыЦена" Тогда
		Если Колонка = "ТоварыЦенаБазовая" Тогда
			Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
			
			Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаБазовая, Строка.СтавкаНДС);
			Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		Иначе
			Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
			
			Строка.ЦенаБазовая = АрифметическиеФункции.ВычитатьНаценку(Строка.ЦенаПродажная, Строка.СтавкаНДС);
			Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
		КонецЕсли;
	КонецЕсли;
	
	// Сумма
	Если Лев(Колонка, 11) = "ТоварыСумма" Тогда
		Если Строка.Количество = 0 Тогда
			Строка.СуммаБазовая = 0;
			Строка.СуммаНДС = 0;
			Строка.СуммаПродажная = 0;
		Иначе
	        Если Колонка = "ТоварыСуммаБазовая" Тогда
				Строка.ЦенаБазовая = Строка.СуммаБазовая / Строка.Количество;
				
				Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаБазовая, Строка.СтавкаНДС);
				Строка.СуммаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.СуммаБазовая, Строка.СтавкаНДС);
				
				Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
			Иначе
				Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
				
				Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
				Строка.СуммаБазовая = АрифметическиеФункции.ВычитатьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
				
				Строка.ЦенаБазовая = Строка.СуммаБазовая / Строка.Количество;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Строка.Объем = Строка.Товар.Объем * Строка.Количество;
	Строка.Вес = Строка.Товар.Вес * Строка.Количество;
КонецПроцедуры

// Вспомагательные процедуры
&НаКлиенте
Процедура ОбновитьИтоги()
	
КонецПроцедуры
