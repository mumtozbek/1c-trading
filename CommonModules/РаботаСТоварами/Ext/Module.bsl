
Функция ПолучитьТаблицуОстатков(Момент, Параметры, Обновление = Ложь) Экспорт
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Товары.Ссылка КАК Товар,
		|
		|	ЕСТЬNULL(РегСтавкаНДС.Значение, ЕСТЬNULL(РегУчетнаяПолитика.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС))) КАК СтавкаНДС,
		|
		|	ЕСТЬNULL(РегОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(РегОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РегОстатки.КоличествоОстаток, 0) <> 0 И ЕСТЬNULL(РегОстатки.СуммаОстаток, 0) <> 0 ТОГДА
		|			ВЫБОР
		|				КОГДА (РегОстатки.СуммаОстаток / РегОстатки.КоличествоОстаток) > 0 ТОГДА
		|					(РегОстатки.СуммаОстаток / РегОстатки.КоличествоОстаток)
		|				ИНАЧЕ
		|					ЕСТЬNULL(РегЦенаПоследняя.Цена, ЕСТЬNULL(РегЦенаСледующая.Цена, 0))
		|			КОНЕЦ
		|		ИНАЧЕ
		|			ВЫБОР
		|				КОГДА &ВидДокумента = ""РеализацияТМЦ"" ТОГДА
		|					ЕСТЬNULL(РегЦенаСледующая.Цена, ЕСТЬNULL(РегЦенаПоследняя.Цена, 0))
		|				ИНАЧЕ
		|					ЕСТЬNULL(РегЦенаПоследняя.Цена, ЕСТЬNULL(РегЦенаСледующая.Цена, 0))
		|			КОНЕЦ
		|	КОНЕЦ КАК ЦенаБазовая,
		|	
		|	ЕСТЬNULL(РегЦенаСледующая.Цена, 0) КАК ЦенаСледующая,
		|
		|	ЕСТЬNULL(РегЦенаПродажная.Цена, 0) КАК ЦенаПродажная,
		|
		|	ЕСТЬNULL(РегСкидкиТоваров.Скидка, 0) КАК Скидка
		|	
		|ИЗ
		|	Справочник.Товары КАК Товары
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Остатки.Остатки(&Момент, Склад В (&Склад) И Порожняя = &Порожняя) КАК РегОстатки ПО (РегОстатки.Товар = Товары.Ссылка)
		
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныТоваров.СрезПоследних(&Дата, Тип = ""ЦенаБазовая"") КАК РегЦенаПоследняя ПО (РегЦенаПоследняя.Товар = Товары.Ссылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныТоваров.СрезПервых(&Дата, Тип = ""ЦенаБазовая"") КАК РегЦенаСледующая ПО (РегЦенаСледующая.Товар = Товары.Ссылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныТоваров.СрезПоследних(&Дата, Тип = ""ЦенаПродажная"") КАК РегЦенаПродажная ПО (РегЦенаПродажная.Товар = Товары.Ссылка)
		
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРеквизитов.СрезПоследних(&Дата, Реквизит = ""СтавкаНДС"") КАК РегСтавкаНДС ПО (РегСтавкаНДС.Ссылка = Товары.Ссылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеПолитики.СрезПоследних(&Дата, Организация = &Организация) КАК РегУчетнаяПолитика ПО (РегУчетнаяПолитика.СтавкаНДС.Ставка > 0)
		
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СкидкиТоваров.СрезПоследних(&Дата, Контрагент = &Контрагент ИЛИ Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК РегСкидкиТоваров ПО (РегСкидкиТоваров.Товар = Товары.Ссылка)
		|ГДЕ
		|	Товары.ЭтоГруппа = ЛОЖЬ И (Товары.Ссылка В (&МассивТоваров) ИЛИ &Список = ИСТИНА ИЛИ (&Обновление = Ложь И РегОстатки.КоличествоОстаток <> 0 И Товары.ВидТовара.ТипТовара В (&ТипТовара)))
		|УПОРЯДОЧИТЬ ПО
		|	Товары.ПорядокСортировки
		|"
	);
	
	Запрос.УстановитьПараметр("ВидДокумента", Параметры.ВидДокумента);
	Запрос.УстановитьПараметр("Момент", Момент);
	Запрос.УстановитьПараметр("Дата", ?(ТипЗнч(Момент) = Тип("Граница"), Момент.Значение.Дата, Момент));
	
	Запрос.УстановитьПараметр("Организация", ?(Параметры.Свойство("Организация"), Параметры.Организация, ПараметрыСеанса.Пользователь.Организация));
	Запрос.УстановитьПараметр("Склад", ?(Параметры.Свойство("Склад"), Параметры.Склад, ПараметрыСеанса.Пользователь.Склад));
	Запрос.УстановитьПараметр("Контрагент", ?(Параметры.Свойство("Контрагент"), Параметры.Контрагент, Справочники.Контрагенты.ПустаяСсылка()));
	
	Запрос.УстановитьПараметр("МассивТоваров", ?(Параметры.Свойство("ВыбранныеТовары"), Параметры.ВыбранныеТовары, Новый Массив));
	Запрос.УстановитьПараметр("ТипТовара", ?(Параметры.Свойство("ТипТовара"), Параметры.ТипТовара, Перечисления.ТипыТовара.Товар));
	Запрос.УстановитьПараметр("ТипСтоимости", ?(Параметры.Свойство("ТипСтоимости"), Параметры.ТипСтоимости, "ЦенаБазовая"));
	
	Запрос.УстановитьПараметр("Обновление", ?(Параметры.Свойство("Обновление"), Параметры.Обновление, Ложь));
	Запрос.УстановитьПараметр("Порожняя", ?(Параметры.Свойство("Порожняя"), Параметры.Порожняя, Ложь));
	Запрос.УстановитьПараметр("Список", ?(Параметры.Свойство("Список"), Параметры.Список, Ложь));
	
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	Если ТипЗнч(Параметры.ВыбранныеТовары) = Тип("СправочникСсылка.Товары") Тогда
		Возврат Результат.Найти(Параметры.ВыбранныеТовары, "Товар");
	Иначе
		Возврат Результат;
	КонецЕсли;
КонецФункции

Процедура Заполнить(Объект, ИмяТаблицы, Обновление = Ложь) Экспорт
	Момент = ОбщийМодульСервер.ПолучитьМоментВремениДокумента(Объект);
	Таблица = Объект[ИмяТаблицы].Выгрузить();
	Параметры = Новый Структура("Склад,ВыбранныеТовары,ТипТовара,ВидДокумента", Объект.Склад, Таблица.ВыгрузитьКолонку("Товар"), Перечисления.ТипыТовара.Товар, Объект.Ссылка.Метаданные().Имя);
	
	Если ТипЗнч(Объект[ИмяТаблицы]) = Тип("ТаблицаЗначений") Тогда
        Остатки = ПолучитьТаблицуОстатков(Момент, Параметры, Обновление);
		Для Каждого Позиция Из Остатки Цикл
			ТекущиеСтроки = Таблица.НайтиСтроки(Новый Структура("Товар", Позиция.Товар));
			
			Если ТекущиеСтроки.Количество() = 0 И Обновление = Ложь Тогда
				Строка = Таблица.Добавить();
				Строка.Товар = Позиция.Товар;
				Строка.Скидка = Позиция.Скидка;
				
				ТекущиеСтроки = Таблица.НайтиСтроки(Новый Структура("Товар", Позиция.Товар));
			КонецЕсли;
			
			Строка.ОстатокДо = Позиция.КоличествоОстаток;
		КонецЦикла;
	Иначе
		Если Параметры.ВидДокумента = "РеализацияТМЦ" Тогда
			Таблица.Свернуть("Товар,СтавкаНДС,Скидка", "Количество,КоличествоУпаковок,СуммаБазовая,СуммаСНаценкой,СуммаНДС,СуммаПродажная,СуммаОтпускная,БонусКоличество,БонусСумма");
		ИначеЕсли Параметры.ВидДокумента = "ПеремещениеТМЦ" Тогда
			Таблица.Свернуть("Товар,СтавкаНДС,Скидка", "Количество,СуммаБазовая,СуммаСНаценкой,СуммаНДС,СуммаПродажная,СуммаОтпускная");
		ИначеЕсли Параметры.ВидДокумента = "ВозвратТМЦПокупателя" Тогда
			Таблица.Свернуть("Товар,СтавкаНДС", "Количество,СуммаБазовая,СуммаСНаценкой,СуммаНДС,СуммаПродажная");
		ИначеЕсли Параметры.ВидДокумента = "ВозвратТМЦПоставщику" Тогда
			Таблица.Свернуть("Товар,СтавкаНДС", "Количество,СуммаБазовая,СуммаНДС,СуммаПродажная");
		ИначеЕсли Параметры.ВидДокумента = "АктСписанияТМЦ" Тогда
			Таблица.Свернуть("Товар,СтавкаНДС", "Количество,СуммаБазовая,СуммаНДС,СуммаПродажная");
		КонецЕсли;
		
		Объект[ИмяТаблицы].Загрузить(Таблица);
		
		Если Параметры.ВидДокумента = "РеализацияТМЦ" Тогда
			Таблица = Объект[ИмяТаблицы].Выгрузить();
		ИначеЕсли Параметры.ВидДокумента = "ПеремещениеТМЦ" Тогда
			Таблица = Объект[ИмяТаблицы].Выгрузить();
		ИначеЕсли Параметры.ВидДокумента = "ВозвратТМЦПокупателя" Тогда
			Таблица = Объект[ИмяТаблицы].Выгрузить();
		ИначеЕсли Параметры.ВидДокумента = "ВозвратТМЦПоставщику" Тогда
			Таблица = Объект[ИмяТаблицы].Выгрузить();
		ИначеЕсли Параметры.ВидДокумента = "АктСписанияТМЦ" Тогда
			Таблица = Объект[ИмяТаблицы].Выгрузить();
		КонецЕсли;
		
		Для Каждого Строка Из Таблица Цикл
			Если Строка.Количество <> 0 Тогда
				Если Таблица.Колонки.Найти("ЦенаОтпускная") <> Неопределено Тогда
					Строка.ЦенаОтпускная = Строка.СуммаОтпускная / Строка.Количество;
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ЦенаПродажная") <> Неопределено Тогда
					Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ЦенаСНаценкой") <> Неопределено Тогда
					Строка.ЦенаСНаценкой = Строка.СуммаСНаценкой / Строка.Количество;
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ЦенаБазовая") <> Неопределено Тогда
					Строка.ЦенаБазовая = Строка.СуммаБазовая / Строка.Количество;
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("Наценка") <> Неопределено Тогда
					Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("Скидка") <> Неопределено Тогда
					Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Остатки = ПолучитьТаблицуОстатков(Момент, Параметры, Обновление);
		Для Каждого Позиция Из Остатки Цикл
			ТекущиеСтроки = Таблица.НайтиСтроки(Новый Структура("Товар", Позиция.Товар));
			
			Если ТекущиеСтроки.Количество() = 0 И Обновление = Ложь Тогда
				Строка = Таблица.Добавить();
				Строка.Товар = Позиция.Товар;
				
				Если Таблица.Колонки.Найти("Скидка") <> Неопределено Тогда
					Строка.Скидка = Мин(Позиция.Скидка, Позиция.Товар.МаксимальнаяСкидка);
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ЦенаПродажная") <> Неопределено Тогда
					Строка.ЦенаПродажная = Позиция.ЦенаПродажная;
				КонецЕсли;
				
				ТекущиеСтроки = Таблица.НайтиСтроки(Новый Структура("Товар", Позиция.Товар));
			КонецЕсли;
			
			КоличествоТекущийОстаток = Позиция.КоличествоОстаток;
			СуммаТекущийОстаток = Позиция.СуммаОстаток;
			
			Для Каждого Строка Из ТекущиеСтроки Цикл
				Если Таблица.Колонки.Найти("ОстатокДо") <> Неопределено Тогда
					Строка.ОстатокДо = КоличествоТекущийОстаток;
				КонецЕсли;
				
				// Реквизиты
				Если Таблица.Колонки.Найти("СтавкаНДС") <> Неопределено Тогда
					Строка.СтавкаНДС = Позиция.СтавкаНДС;
				КонецЕсли;
				
			    // Цены
				Если Таблица.Колонки.Найти("ЦенаБазовая") <> Неопределено Тогда
					Строка.ЦенаБазовая = Позиция.ЦенаБазовая;
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ЦенаОтпускная") <> Неопределено Тогда
					Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ЦенаСНаценкой") <> Неопределено Тогда
					Строка.ЦенаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.ЦенаПродажная, Строка.СтавкаНДС);
				КонецЕсли;
				
				// Наценка и скидка
				Если Таблица.Колонки.Найти("Наценка") <> Неопределено Тогда
					Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("Скидка") <> Неопределено Тогда
					Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
				КонецЕсли;
				
				// Разбиваем на строки, где остаток по текущей цене имеется, но не хватает
				Если Строка.Количество > 0 И Окр(Позиция.ЦенаСледующая, 2) <> 0 И Окр(Позиция.ЦенаСледующая, 2) <> Окр(Позиция.ЦенаБазовая, 2) Тогда
					Если КоличествоТекущийОстаток > 0 И КоличествоТекущийОстаток < Строка.Количество Тогда
						НоваяСтрока = Таблица.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
						
						НоваяСтрока.ОстатокДо = 0;
						НоваяСтрока.Количество = Строка.Количество - КоличествоТекущийОстаток;
						НоваяСтрока.ЦенаБазовая = Позиция.ЦенаСледующая;
						НоваяСтрока.СуммаПродажная = НоваяСтрока.ЦенаПродажная * НоваяСтрока.Количество;
						НоваяСтрока.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(НоваяСтрока.СуммаПродажная, НоваяСтрока.СтавкаНДС);
						
						Если Таблица.Колонки.Найти("СуммаСНаценкой") <> Неопределено Тогда
							НоваяСтрока.СуммаСНаценкой = НоваяСтрока.ЦенаСНаценкой * НоваяСтрока.Количество;
							НоваяСтрока.СуммаБазовая = НоваяСтрока.ЦенаБазовая * НоваяСтрока.Количество;
							НоваяСтрока.Наценка = АрифметическиеФункции.ОпределитьНаценку(НоваяСтрока.ЦенаСНаценкой, НоваяСтрока.ЦенаБазовая);
						КонецЕсли;
						
						Строка.Количество = КоличествоТекущийОстаток;
						Строка.ЦенаБазовая = Позиция.ЦенаБазовая;
						Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
						Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
						
						Если Таблица.Колонки.Найти("СуммаСНаценкой") <> Неопределено Тогда
							Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
							Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
							Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
						КонецЕсли;
						
						Если Таблица.Колонки.Найти("СуммаОтпускная") <> Неопределено Тогда
							Строка.СуммаОтпускная = Строка.ЦенаОтпускная * Строка.Количество;
							НоваяСтрока.СуммаОтпускная = НоваяСтрока.ЦенаОтпускная * НоваяСтрока.Количество;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				// Если количество обнуляется, обнуляем и сумму
				Если Строка.Количество <> 0 И КоличествоТекущийОстаток = Строка.Количество Тогда
					Строка.ЦенаБазовая = СуммаТекущийОстаток / Строка.Количество;
					Строка.СуммаБазовая = СуммаТекущийОстаток;
				КонецЕсли;
				
				Если Таблица.Колонки.Найти("ОстатокПосле") <> Неопределено Тогда
					Строка.ОстатокПосле = Строка.ОстатокДо - Строка.Количество;
				КонецЕсли;
				
				КоличествоТекущийОстаток = КоличествоТекущийОстаток - Строка.Количество;
				СуммаТекущийОстаток = СуммаТекущийОстаток - Строка.СуммаБазовая;
			КонецЦикла;
		КонецЦикла;
		
		Для Каждого Строка Из Таблица Цикл
			Если Строка.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если Параметры.ВидДокумента = "РеализацияТМЦ" Тогда
				Строка = ПересчитатьРеализацию(Строка, "ТоварыСуммаОтпускная");
			Иначе
				Пересчитать(Строка, "ТоварыКоличество");
			КонецЕсли;
		КонецЦикла;
		
		Объект[ИмяТаблицы].Загрузить(Таблица);
	КонецЕсли;
КонецПроцедуры

Процедура Пересчитать(ЗНАЧ Данные, ЗНАЧ Колонка = "ТоварыКоличество") Экспорт
	Если ТипЗнч(Данные) = Тип("СтрокаТаблицыЗначений") Тогда
		Строка = РаботаСТаблицами.ПреобразоватьСтрокуТаблицыЗначенийВСтруктуру(Данные);
	Иначе
		Строка = Данные;
	КонецЕсли;
	
	Если Строка.Свойство("КоличествоУпаковок") Тогда
		Строка.КоличествоУпаковок = Строка.Количество / Строка.Товар.ВместимостьУпаковки;
	КонецЕсли;
	
	Если Строка.Свойство("ОстатокПосле") Тогда
		Строка.ОстатокПосле = Строка.ОстатокДо - Строка.Количество;
	КонецЕсли;
	
	// Количество
	Если Колонка = "ТоварыКоличество" Тогда
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
		Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
		Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
		Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
		
		Колонка = "ТоварыНаценка";
	КонецЕсли;
	
	Если Строка.ЦенаБазовая <> 0 Тогда
		// Наценка
		Если Колонка = "ТоварыНаценка" Тогда
			Строка.ЦенаСНаценкой = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаБазовая, Строка.Наценка);
			Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
			
			Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
			Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		КонецЕсли;
		
		// СтавкаНДС
		Если Колонка = "ТоварыСтавкаНДС" Тогда
			Если Строка.ЦенаПродажная <> 0 Тогда
				Строка.ЦенаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.ЦенаПродажная, Строка.СтавкаНДС);
				Строка.СуммаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
				
				Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.СуммаСНаценкой, Строка.СуммаБазовая);
				
				Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
			Иначе
				Строка.ЦенаСНаценкой = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаБазовая, Строка.Наценка);
				Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
				
				Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
				Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Строка.ЦенаПродажная <> 0 Тогда
		Строка.ЦенаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.ЦенаПродажная, Строка.СтавкаНДС);
		Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
		
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
	ИначеЕсли Строка.ЦенаСНаценкой <> 0 Тогда
		Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		
		Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
	КонецЕсли;
	
	// Цена
	Если Лев(Колонка, 10) = "ТоварыЦена" Тогда
		Если Колонка = "ТоварыЦенаБазовая" Тогда
			Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
			
			Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
		ИначеЕсли Колонка = "ТоварыЦенаСНаценкой" Тогда
			Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
			
			Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
			
			Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
		ИначеЕсли Колонка = "ТоварыЦенаСНДС" Тогда
			Строка.ЦенаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.ЦенаПродажная, Строка.СтавкаНДС);
			
			Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
			
			Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		КонецЕсли;
	КонецЕсли;
	
	// Сумма
	Если Строка.Количество = 0 Тогда
		Строка.СуммаБазовая = 0;
		Строка.СуммаСНаценкой = 0;
		Строка.СуммаНДС = 0;
		Строка.СуммаПродажная = 0;
	ИначеЕсли Лев(Колонка, 11) = "ТоварыСумма" Тогда
        Если Колонка = "ТоварыСуммаБазовая" Тогда
			Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.СуммаСНаценкой, Строка.СуммаБазовая);
			
			Строка.ЦенаБазовая = Строка.СуммаБазовая / Строка.Количество;
		ИначеЕсли Колонка = "ТоварыСуммаСНаценкой" Тогда
			Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
			
			Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.СуммаСНаценкой, Строка.СуммаБазовая);
			
			Строка.СуммаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
			
			Строка.ЦенаСНаценкой = Строка.СуммаСНаценкой / Строка.Количество;
		ИначеЕсли Колонка = "ТоварыСуммаСНДС" Тогда
			Строка.СуммаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
			
			Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.СуммаСНаценкой, Строка.СуммаБазовая);
			
			Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
		КонецЕсли;
	КонецЕсли;
	
	Если Строка.ЦенаПродажная <> 0 И Строка.СуммаПродажная = 0 Тогда
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
	КонецЕсли;
	
	Если Строка.ЦенаСНаценкой <> 0 И Строка.СуммаСНаценкой = 0 Тогда
		Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
	КонецЕсли;
		
	Если Строка.ЦенаБазовая <> 0 И Строка.СуммаБазовая = 0 Тогда	
		Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
	КонецЕсли;
	
	Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
	
	Строка.Объем = Строка.Товар.Объем * Строка.Количество;
	Строка.Вес = Строка.Товар.Вес * Строка.Количество;
	
	Если ТипЗнч(Данные) = Тип("СтрокаТаблицыЗначений") Тогда
		Для Каждого Реквизит Из Строка Цикл
			Данные[Реквизит.Ключ] = Реквизит.Значение;
		КонецЦикла;
	Иначе
		Данные = Строка;
	КонецЕсли;
КонецПроцедуры

Функция ПересчитатьЗаказа(ЗНАЧ Строка, ЗНАЧ Колонка = "ТоварыЦенаПродажная") Экспорт
	Если Колонка = Неопределено Тогда
		Колонка = "ТоварыЦенаПродажная";
	КонецЕсли;
	
	Если Строка.ЦенаОтпускная = 0 Тогда
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Если Строка.СуммаОтпускная = 0 Тогда
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Если Колонка = "ТоварыКоличество" Тогда
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		Строка.СуммаОтпускная = Строка.ЦенаОтпускная * Строка.Количество;
	КонецЕсли;
	
	Если Колонка = "ТоварыЦенаПродажная" Тогда
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
		
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
	КонецЕсли;
	
	Если Колонка = "ТоварыСкидка" Тогда
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Если Колонка = "ТоварыЦенаОтпускная" Тогда
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
		Строка.СуммаОтпускная = Строка.ЦенаОтпускная * Строка.Количество;
	КонецЕсли;
	
	Если Строка.Количество = 0 Тогда
		Строка.СуммаПродажная = 0;
		Строка.СуммаОтпускная = 0;
	Иначе
		Если Колонка = "ТоварыСуммаПродажная" Тогда
			Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
			
			Возврат ПересчитатьЗаказа(Строка, "ТоварыЦенаПродажная");
		ИначеЕсли Колонка = "ТоварыСуммаОтпускная" Тогда
			Строка.ЦенаОтпускная = Строка.СуммаОтпускная / Строка.Количество;
			
			Возврат ПересчитатьЗаказа(Строка, "ТоварыЦенаОтпускная");
		КонецЕсли;
	КонецЕсли;
	
	Если Строка.ЦенаОтпускная = 0 Тогда
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Если Строка.СуммаОтпускная = 0 Тогда
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Строка.Объем = Строка.Товар.Объем * Строка.Количество;
	Строка.Вес = Строка.Товар.Вес * Строка.Количество;
	
	Возврат Строка;
КонецФункции

Функция ПересчитатьРеализацию(ЗНАЧ Строка, ЗНАЧ Колонка = "ТоварыЦенаПродажная") Экспорт
	Если Колонка = Неопределено Тогда
		Колонка = "ТоварыЦенаПродажная";
	КонецЕсли;
	
	Если Колонка = "ТоварыКоличество" Тогда
		Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
		Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
		Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		Строка.СуммаОтпускная = Строка.ЦенаОтпускная * Строка.Количество;
	КонецЕсли;
	
	Если Колонка = "ТоварыНаценка" Тогда
		Строка.ЦенаСНаценкой = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаБазовая, Строка.Наценка);
		Строка.СуммаСНаценкой = АрифметическиеФункции.ДобавитьНаценку(Строка.СуммаБазовая, Строка.Наценка);
		
		Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		
		Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
		Строка.СуммаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
	КонецЕсли;
	
	Если Колонка = "ТоварыЦенаБазовая" Тогда
		Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
		
		Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
	КонецЕсли;
	
	Если Колонка = "ТоварыЦенаСНаценкой" Тогда
		Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
		
		Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
		
		Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		
		Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
		Строка.СуммаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
	КонецЕсли;
	
	Если Колонка = "ТоварыСтавкаНДС" Тогда
		Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		
		Строка.ЦенаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.ЦенаСНаценкой, Строка.СтавкаНДС);
		Строка.СуммаПродажная = АрифметическиеФункции.ДобавитьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
		
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
	КонецЕсли;
	
	Если Колонка = "ТоварыЦенаПродажная" Тогда
		Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
		
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
		
		Строка.ЦенаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.ЦенаПродажная, Строка.СтавкаНДС);
		Строка.СуммаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
		
		Строка.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Строка.СуммаПродажная, Строка.СтавкаНДС);
		
		Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
		
		Строка.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
		
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
	КонецЕсли;
	
	Если Колонка = "ТоварыСкидка" Тогда
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Если Колонка = "ТоварыЦенаОтпускная" Тогда
		Строка.Скидка = -АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаОтпускная, Строка.ЦенаПродажная);
		Строка.СуммаОтпускная = Строка.ЦенаОтпускная * Строка.Количество;
	КонецЕсли;
	
	Если Строка.Количество = 0 Тогда
		Строка.СуммаБазовая = 0;
		Строка.СуммаСНаценкой = 0;
		Строка.СуммаНДС = 0;
		Строка.СуммаПродажная = 0;
		Строка.СуммаОтпускная = 0;
	Иначе
		Если Колонка = "ТоварыСуммаБазовая" Тогда
			Строка.ЦенаБазовая = Строка.СуммаБазовая / Строка.Количество;
			
			Возврат ПересчитатьРеализацию(Строка, "ТоварыЦенаБазовая");
		ИначеЕсли Колонка = "ТоварыСуммаСНаценкой" Тогда
			Строка.ЦенаСНаценкой = Строка.СуммаСНаценкой / Строка.Количество;
			
			Возврат ПересчитатьРеализацию(Строка, "ТоварыЦенаСНаценкой");
		ИначеЕсли Колонка = "ТоварыСуммаПродажная" Тогда
			Строка.ЦенаПродажная = Строка.СуммаПродажная / Строка.Количество;
			
			Возврат ПересчитатьРеализацию(Строка, "ТоварыЦенаПродажная");
		ИначеЕсли Колонка = "ТоварыСуммаОтпускная" Тогда
			Строка.ЦенаОтпускная = Строка.СуммаОтпускная / Строка.Количество;
			
			Возврат ПересчитатьРеализацию(Строка, "ТоварыЦенаОтпускная");
		КонецЕсли;
	КонецЕсли;
	
	Если Строка.ЦенаОтпускная = 0 Тогда
		Строка.ЦенаОтпускная = Окр(Строка.ЦенаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Если Строка.СуммаОтпускная = 0 Тогда
		Строка.СуммаОтпускная = Окр(Строка.СуммаПродажная * (100 - Строка.Скидка) / 100, 2);
	КонецЕсли;
	
	Строка.СуммаБазовая = Строка.ЦенаБазовая * Строка.Количество;
	Строка.СуммаСНаценкой = Строка.ЦенаСНаценкой * Строка.Количество;
	Строка.СуммаНДС = АрифметическиеФункции.РассчитатьНаценку(Строка.СуммаСНаценкой, Строка.СтавкаНДС);
	Строка.СуммаПродажная = Строка.ЦенаПродажная * Строка.Количество;
	Строка.СуммаОтпускная = Строка.ЦенаОтпускная * Строка.Количество;
	Строка.Объем = Строка.Товар.Объем * Строка.Количество;
	Строка.Вес = Строка.Товар.Вес * Строка.Количество;
	
	Возврат Строка;
КонецФункции

Функция Остаток(Склад, Товар, Момент, Поле = "Количество", Порожняя = Ложь) Экспорт
	Параметры = Новый Структура("Склад,ВыбранныеТовары,ТипТовара,ВидДокумента", Склад, Товар, Перечисления.ТипыТовара.Товар, "");
	
	Данные = ПолучитьТаблицуОстатков(Момент, Параметры, Истина);
	
	Если ЗначениеЗаполнено(Данные) Тогда
		Возврат Данные[Поле + "Остаток"];
	КонецЕсли;
КонецФункции

Функция Цена(Товар, Тип, Момент, ЗначениеПоУмолчанию = 0) Экспорт
	Данные = РегистрыСведений.ЦеныТоваров.ПолучитьПоследнее(Момент, Новый Структура("Товар,Тип", Товар, Тип));
	
	Возврат ?(Данные.Цена > 0, Данные.Цена, ЗначениеПоУмолчанию);
КонецФункции

Функция Скидка(Товар, Момент, ЗначениеПоУмолчанию = 0) Экспорт
	Данные = РегистрыСведений.СкидкиТоваров.ПолучитьПоследнее(Момент, Новый Структура("Товар,Контрагент", Товар, Справочники.Контрагенты.ПустаяСсылка()));
	
	Возврат ?(Данные.Скидка > 0, Данные.Скидка, ЗначениеПоУмолчанию);
КонецФункции
