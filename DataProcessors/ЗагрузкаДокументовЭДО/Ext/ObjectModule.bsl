
Процедура ЗаполнитьДокументПоступлениеТМЦ(ДокументОбъект, Знач ДокументЭлемент) Экспорт
	ДокументОбъект.Товары.Очистить();
	Для Каждого ТоварЭлемент Из ДокументЭлемент.ProductList.Products Цикл
		Если ТоварЭлемент.Count = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		НоваяСтрока.Товар = НайтиТовара(ТоварЭлемент);
		НоваяСтрока.Количество = ТоварЭлемент.Count;
		
		НоваяСтрока.ЦенаБазовая = ТоварЭлемент.Summa;
		НоваяСтрока.СуммаБазовая = ТоварЭлемент.DeliverySum;
		
		НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.НайтиПоРеквизиту("Ставка",  ТоварЭлемент.VatRate);
		НоваяСтрока.СуммаНДС = ТоварЭлемент.VatSum;

		НоваяСтрока.ЦенаПриходная = ТоварЭлемент.DeliverySumWithVat / ТоварЭлемент.Count;
		НоваяСтрока.СуммаПриходная = ТоварЭлемент.DeliverySumWithVat;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьДокументРеализацияТМЦ(ДокументОбъект, Знач ДокументЭлемент) Экспорт
	ДокументОбъект.Товары.Очистить();
КонецПроцедуры

Функция НайтиКонтрагента(ИНН, ПИНФЛ, Структура) Экспорт
	ЭлементСсылка = Справочники.Контрагенты.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		ЭлементСсылка = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", ИНН);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПИНФЛ) Тогда
		Если ЗначениеЗаполнено(ЭлементСсылка) = Ложь Тогда
			ЭлементСсылка = Справочники.Контрагенты.НайтиПоРеквизиту("ПИНФЛ", ПИНФЛ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементСсылка) = Ложь Тогда
		ЭлементСсылка = Справочники.Контрагенты.НайтиПоРеквизиту("ПолноеНаименование", Структура.Name);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементСсылка) = Ложь Тогда
		ЭлементОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		ЭлементОбъект.ИНН = ИНН;
		ЭлементОбъект.ПИНФЛ = ПИНФЛ;
		ЭлементОбъект.Наименование = Структура.Name;
		ЭлементОбъект.ПолноеНаименование = Структура.Name;
		
		ЭлементОбъект.ПлательщикНДС = (Структура.VatRegStatus = 20);
		
		Если ЭлементОбъект.ПлательщикНДС Тогда
			ЭлементОбъект.КодПНДС = Структура.VatRegCode;
		КонецЕсли;
		
		ЭлементОбъект.КодОКЭД = Структура.Oked;
		ЭлементОбъект.Адрес = Структура.Address;
		ЭлементОбъект.Руководитель = Структура.Director;
		ЭлементОбъект.Бухгалтер = Структура.Accountant;
		ЭлементОбъект.Телефон = Структура.WorkPhone;
		
		ЭлементОбъект.Записать();
		
		ЭлементСсылка = ЭлементОбъект.Ссылка;
	КонецЕсли;
	
	Возврат ЭлементСсылка;
КонецФункции

Функция НайтиДоговора(Владелец, Номер, Дата, ВидДоговора) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка КАК Договор ИЗ Справочник.Договоры КАК Договоры ГДЕ Договоры.Владелец = &Владелец И Договоры.Номер = &Номер И Договоры.Дата = &Дата И Договоры.ВидДоговора = &ВидДоговора");
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Номер", Номер);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВидДоговора", ВидДоговора);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Договор;
	Иначе
		ЭлементОбъект = Справочники.Договоры.СоздатьЭлемент();
		ЭлементОбъект.Номер = Номер;
		ЭлементОбъект.Дата = Дата;
		ЭлементОбъект.ВидДоговора = ВидДоговора;
		ЭлементОбъект.Владелец = Владелец;
		ЭлементОбъект.Записать();
		
		Возврат ЭлементОбъект.Ссылка
	КонецЕсли;
КонецФункции

Функция НайтиТовара(Структура, Поставщик = Неопределено) Экспорт
	ЭлементСсылка = Справочники.Товары.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Структура.BarCode) Тогда
		ЭлементСсылка = Справочники.Товары.НайтиПоРеквизиту("ШтрихКод", Структура.BarCode);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Структура.CatalogCode) Тогда
		Если ЗначениеЗаполнено(ЭлементСсылка) = Ложь Тогда
			ЭлементСсылка = Справочники.Товары.НайтиПоРеквизиту("КодИКПУ", Структура.CatalogCode);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементСсылка) = Ложь Тогда
		ЭлементСсылка = Справочники.Товары.НайтиПоРеквизиту("ПолноеНаименование", Структура.Name);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементСсылка) = Ложь Тогда
		ЭлементОбъект = Справочники.Товары.СоздатьЭлемент();
		ЭлементОбъект.ШтрихКод = Структура.BarCode;
		ЭлементОбъект.КодИКПУ = Структура.CatalogCode;
		ЭлементОбъект.Наименование = Структура.Name;
		ЭлементОбъект.ПолноеНаименование = Структура.Name;
		ЭлементОбъект.Поставщик = Поставщик;
		ЭлементОбъект.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.Штуки;
		ЭлементОбъект.ВидТовара = Справочники.ВидыТовара.Товар;
		
		ЭлементОбъект.Записать();
		
		ЭлементСсылка = ЭлементОбъект.Ссылка;
	КонецЕсли;
	
	Возврат ЭлементСсылка;
КонецФункции

Функция ПолучитьДату(ЗНАЧ Строка) Экспорт
	Строка = СтрЗаменить(Строка, ".", "");
	Строка = СтрЗаменить(Строка, " ", "");
	Строка = СтрЗаменить(Строка, ":", "");
	Строка = СтрЗаменить(Строка, "-", "");

	Попытка
		Возврат Дата(Строка);
	Исключение
		Возврат Дата(Строка + "000000");
	КонецПопытки;
КонецФункции
