
Процедура Заполнить(СписокДокументов, Период, Организация) Экспорт
	СписокДокументов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка,
	|	Ссылка.Дата
	|ИЗ
	|	Документ.РеализацияТМЦ
	|ГДЕ
	|	Ссылка.Проведен = Истина
	|	И Ссылка.ВидРеализации.ТипВзаиморасчета = ЗНАЧЕНИЕ(Перечисление.ТипыВзаиморасчета.Безнал)
	|	И Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|	И Ссылка.Организация = &Организация
	|СГРУППИРОВАТЬ ПО
	|	Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка.Дата";
	
	Результат = Запрос.Выполнить();
	ВыборкаДокумент = Результат.Выбрать(ОбходРезультатаЗапроса.Прямой); 
	Пока ВыборкаДокумент.Следующий() Цикл
		Строка = СписокДокументов.Добавить();
		Строка.Экспортировать = Истина;
		Строка.Документ = ВыборкаДокумент.Ссылка;
		Строка.Контрагент = ВыборкаДокумент.Ссылка.Контрагент;
		Строка.Сумма = ВыборкаДокумент.Ссылка.Сумма;
	КонецЦикла;
КонецПроцедуры

Процедура Сформировать(ТабДок, СписокДокументов) Экспорт
	СписокДокументовНаЭкспорт = СписокДокументов.НайтиСтроки(Новый Структура("Экспортировать", Истина));
	Если СписокДокументовНаЭкспорт.Количество() = 0 Тогда
		Сообщить("Не отмечен ни один документ на экспорт!");
	КонецЕсли;
	
	Индекс = 0;
	
	// Настройка макета
	Макет = ПолучитьМакет("Основной");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьДокумент = Макет.ПолучитьОбласть("Документ");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьОкругление = Макет.ПолучитьОбласть("Округление");
	
	// Шапка
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// Строки
	Для Каждого СтрокаИзСпискаДокументов из СписокДокументовНаЭкспорт Цикл
		Выборка = СтрокаИзСпискаДокументов.Документ;
		Индекс = Индекс + 1;

		Для Каждого Строка Из Выборка.Товары Цикл
			УчетнаяПолитика = УчетныеПолитики.ПолучитьНаДату(Выборка.Дата, Выборка.Организация);
			
			Если Строка.НомерСтроки = 1 Тогда
				Область = ОбластьДокумент;
				
				Область.Параметры.Заполнить(Выборка);
				Область.Параметры.Индекс = Индекс;
				Область.Параметры.НомерДоговора = Выборка.Договор.Номер;
				Область.Параметры.ДатаДоговора = Выборка.Договор.Дата;
				Область.Параметры.НомерДоверенности = Выборка.НомерДоверенности;
				Область.Параметры.ДатаДоверенности = Выборка.ДатаДоверенности;
				Область.Параметры.МОЛ = Выборка.Склад.МОЛ;
				
				Область.Параметры.НаименованиеОрганизации = Выборка.Организация.Наименование;
				Область.Параметры.ИННОрганизации = Выборка.Организация.ИНН;
				Область.Параметры.СчетОрганизации = Выборка.Организация.ОсновнойБанковскийСчет.Номер;
				Область.Параметры.МФООрганизации = Выборка.Организация.ОсновнойБанковскийСчет.Банк.Код;
				Область.Параметры.АдресОрганизации = Выборка.Организация.Адрес;
				Область.Параметры.ТелефонОрганизации = Выборка.Организация.Телефон;
				Область.Параметры.ОКЭДОрганизации = Выборка.Организация.КодОКЭД;
				Область.Параметры.КодРайонаОрганизации = Выборка.Организация.Район.Код;
				Область.Параметры.ДиректорОрганизации = УчетнаяПолитика.Руководитель;
				Область.Параметры.БухгалтерОрганизации = УчетнаяПолитика.Бухгалтер;
				Область.Параметры.КодПНДСОрганизации = Выборка.Организация.КодПНДС;
				
				Область.Параметры.НаименованиеКонтрагента = Выборка.Контрагент.Наименование;
				Область.Параметры.ИННКонтрагента = Выборка.Контрагент.ИНН;
				Область.Параметры.СчетКонтрагента = Выборка.Контрагент.ОсновнойБанковскийСчет.Номер;
				Область.Параметры.МФОКонтрагента = Выборка.Контрагент.ОсновнойБанковскийСчет.Банк.Код;
				Область.Параметры.АдресКонтрагента = Выборка.Контрагент.Адрес;
				Область.Параметры.ТелефонКонтрагента = Выборка.Контрагент.Телефон;
				Область.Параметры.ОКЭДКонтрагента = Выборка.Контрагент.КодОКЭД;
				Область.Параметры.КодРайонаКонтрагента = Выборка.Контрагент.Район.Код;
				Область.Параметры.ДиректорКонтрагента = Выборка.Контрагент.Руководитель;
				Область.Параметры.БухгалтерКонтрагента = Выборка.Контрагент.Бухгалтер;
				Область.Параметры.КодПНДСКонтрагента = Выборка.Контрагент.КодПНДС;
			ИначеЕсли Строка.НомерСтроки > 1 Тогда
				Область = ОбластьСтрока;
			КонецЕсли;
			
			Область.Параметры.НомерСтроки = Строка.НомерСтроки;
			Область.Параметры.Товар = Строка.Товар.Наименование;
			Область.Параметры.КодИКПУ = ?(ЗначениеЗаполнено(Строка.Товар.КодИКПУ), Строка.Товар.КодИКПУ, Строка.Товар.ВидТовара.КодИКПУ);
			Область.Параметры.ШтрихКод = Строка.Товар.ШтрихКод;
			Область.Параметры.ЕдиницаИзмерения = Строка.Товар.ЕдиницаИзмерения.Код;
			Область.Параметры.Количество = Строка.Количество;
			Область.Параметры.ЦенаБазовая = Строка.ЦенаБазовая;
			Область.Параметры.Наценка = АрифметическиеФункции.ОпределитьНаценку(Строка.ЦенаСНаценкой, Строка.ЦенаБазовая);
			Область.Параметры.ЦенаСНаценкой = Строка.ЦенаСНаценкой;
			Область.Параметры.СуммаСНаценкой = Строка.СуммаСНаценкой;
			Область.Параметры.СтавкаНДС = Строка.СтавкаНДС.Ставка;
			Область.Параметры.СуммаНДС = Строка.СуммаНДС;
			Область.Параметры.СуммаПродажная = Строка.СуммаПродажная;
			
			ТабДок.Вывести(Область);
		КонецЦикла;

		Если Выборка.Округление <> 0 Тогда
			ОбластьОкругление.Параметры.НомерСтроки = Строка.НомерСтроки + 1;
			ОбластьОкругление.Параметры.КодИКПУ = ?(Выборка.Округление > 0, Справочники.ВидыТовара.НакладныеРасходы.КодИКПУ, Справочники.ВидыТовара.ФинансоваяСкидка.КодИКПУ);
			ОбластьОкругление.Параметры.ЦенаБазовая = АрифметическиеФункции.ВычитатьНаценку(Выборка.Округление, УчетнаяПолитика.СтавкаНДС.Ставка);
			ОбластьОкругление.Параметры.ЦенаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Выборка.Округление, УчетнаяПолитика.СтавкаНДС.Ставка);
			ОбластьОкругление.Параметры.СуммаСНаценкой = АрифметическиеФункции.ВычитатьНаценку(Выборка.Округление, УчетнаяПолитика.СтавкаНДС.Ставка);
			ОбластьОкругление.Параметры.СтавкаНДС = УчетнаяПолитика.СтавкаНДС.Ставка;
			ОбластьОкругление.Параметры.СуммаНДС = АрифметическиеФункции.ВыделитьНаценку(Выборка.Округление, УчетнаяПолитика.СтавкаНДС.Ставка);
			ОбластьОкругление.Параметры.СуммаПродажная = Выборка.Округление;
			ТабДок.Вывести(ОбластьОкругление);
		КонецЕсли;
	КонецЦикла;
	
	// Вывод заполненной формы
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры
