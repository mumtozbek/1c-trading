&НаКлиенте
Процедура ТаблицаВидыОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если СтрДлина(ТипОбъекта) = 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Укажите тип объекта";
		Сообщение.Поле = "ТипОбъекта";
		Сообщение.Сообщить();
		
	Иначе 
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ТипОбъекта", ТипОбъекта);
		ПараметрыОткрытия.Вставить("ТаблицаВидыОбъектов", ТаблицаВидыОбъектов);
		ПараметрыОткрытия.Вставить("ОбрабатыватьТЧ", ОбрабатыватьТЧ);
		
		ФормаВыбораВидовОбъектов = ПолучитьФорму("Обработка.ГрупповаяОбработкаСправочниковИДокументов.Форма.ФормаВыбораТаблицы", ПараметрыОткрытия);
		
		РезультатВыбора = ФормаВыбораВидовОбъектов.ОткрытьМодально();
		
		Если РезультатВыбора <> Неопределено Тогда
			
			Если ОбрабатыватьТЧ = Ложь Тогда 
				
				ТаблицаМетаданных.Очистить();
				ТаблицаВидыОбъектов.Очистить();
				
				Для Каждого ЭлементРезультат Из РезультатВыбора Цикл
					
					НоваяСтрока = ТаблицаВидыОбъектов.Добавить();
					НоваяСтрока.ИмяТаблицы = ЭлементРезультат.Значение;
					НоваяСтрока.ПредставлениеТаблицы = ЭлементРезультат.Представление;
					
				КонецЦикла;
				
				ТаблицаРезультатОтбор.Очистить();
				
				ИнициализироватьСКД();
				
				ВыберитеДействие = "";
				
			Иначе
				
				ТаблицаМетаданных.Очистить();
				ТаблицаВидыОбъектов.Очистить();
				ТаблицаРезультатОтбор.Очистить();
				
				Для Каждого ЭлементРезультат Из РезультатВыбора Цикл
					
					НоваяСтрока = ТаблицаВидыОбъектов.Добавить();
					НоваяСтрока.ИмяТаблицы = ЭлементРезультат.Значение.ИмяОбъекта;
					НоваяСтрока.ИмяТЧ = ЭлементРезультат.Значение.ИмяТЧ;
					НоваяСтрока.ПредставлениеТаблицы = ЭлементРезультат.Представление;
					
				КонецЦикла;
				
				ИнициализироватьСКД();
				
			КонецЕсли;
			
		КонецЕсли;
		
		ИнициализироватьДействия();
		
	КонецЕсли;
	
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьДействия()
	
	НесколькоРеквизитов = Ложь;
	
	Если ВыберитеДействие = "ИзменитьРеквизиты" Тогда
		
		НесколькоРеквизитов = Истина;
		
	КонецЕсли;
	
	Элементы.ВыберитеДействие.СписокВыбора.Очистить();
	
	НовоеДействие = Элементы.ВыберитеДействие.СписокВыбора.Добавить("ИзменитьПометкаУдаления", "Изменить: [Пометка на удаление]");
	НовоеДействие = Элементы.ВыберитеДействие.СписокВыбора.Добавить("ИзменитьРеквизиты", "Изменить: [Реквизиты]");
	
	Если ТипОбъекта = "Документы" Тогда
		
		Элементы.ВыберитеДействие.СписокВыбора.Добавить("ИзменитьПроведение", "Изменить: [Проведение документа]");
		
	ИначеЕсли ТипОбъекта = "Задачи" Тогда
		
		Элементы.ВыберитеДействие.СписокВыбора.Добавить("ВыполнитьЗадачу", "Выполнить задачу");
		
	ИначеЕсли ТипОбъекта = "БизнесПроцессы" Тогда
		
		Элементы.ВыберитеДействие.СписокВыбора.Добавить("Старт", "Стартовать Бизнес-Процесс");
		
	КонецЕсли;
	
	Объект.ФлагИзмененияРеквизитов = Истина; //Затычка
	
	Если Объект.ФлагИзмененияРеквизитов Тогда
		
		Для Каждого ЭлементМетаданных Из ТаблицаМетаданных Цикл
			
			Если ЭлементМетаданных.ИмяРеквизита <> "Ссылка" 
				И ЭлементМетаданных.ИмяРеквизита <> "ИмяТЧ"  
				И ЭлементМетаданных.ИмяРеквизита <> "НомерСтроки"  Тогда
				
				Элементы.ВыберитеДействие.СписокВыбора.Добавить(ЭлементМетаданных.ИмяРеквизита, "Изменить рекзвизит: " + ?(СтрДлина(ЭлементМетаданных.СинонимРеквизита) > 0, ЭлементМетаданных.СинонимРеквизита, ЭлементМетаданных.ИмяРеквизита));
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	 
	УдалитьЭлементНовоеЗначение();
	
	Если НесколькоРеквизитов Тогда
		
		ВыберитеДействие = "ИзменитьРеквизиты";		
		ПодготовитьПараметрыДопРеквизиты();
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементНовоеЗначение()
	
	МассивРеквизитовНаУдаление = Новый Массив;
	МассивЭлементовНаУдаление = Новый Массив;
	
	Для Каждого ЭлементНовоеЗначение Из Элементы.ГруппаНовоеЗначение.ПодчиненныеЭлементы Цикл
		
		Если ЭлементНовоеЗначение.Имя <> "Затычко" Тогда
			
			МассивРеквизитовНаУдаление.Добавить(ЭлементНовоеЗначение.ПутьКДанным);
			МассивЭлементовНаУдаление.Добавить(ЭлементНовоеЗначение);
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Если МассивРеквизитовНаУдаление.Количество() > 0 Тогда
		
		ЭтаФорма.ИзменитьРеквизиты(,МассивРеквизитовНаУдаление);
		
		Для Каждого ЭлементНаУдаление Из МассивЭлементовНаУдаление Цикл
			
			Элементы.Удалить(ЭлементНаУдаление);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСКД()
	
	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;
	НовыйИсточник = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	НовыйИсточник.Имя = "ИсточникДанных1";
	НовыйИсточник.ТипИсточникаДанных = "Local";
	
	НовыйНаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
	НовыйНаборДанных.ИсточникДанных = "Local";
	
	НовыйНаборДанных.Имя = "Основной";
	НовыйНаборДанных.ИмяОбъекта = "Основной"; 	
	НовыйНаборДанных.ИсточникДанных = "ИсточникДанных1";
	
	ТаблицаМетаданных.Очистить();
	
	ТаблицаОбщихРеквизитов = Новый ТаблицаЗначений;
	
	ТаблицаОбщихРеквизитов.Колонки.Добавить("ОбъектМетаданных");
	ТаблицаОбщихРеквизитов.Колонки.Добавить("КоличествоРеквизитов");
	
	МассивТиповСсылка = Новый Массив;
	
	Для Каждого Элементобъектов Из РезультатВыбора Цикл
		
		Если ТипОбъекта = "Справочники" Тогда
			
			СтрокаТипа = "СправочникСсылка.";
			
		ИначеЕсли ТипОбъекта = "Документы" Тогда 
			
			СтрокаТипа = "ДокументСсылка.";
			
		ИначеЕсли ТипОбъекта = "ПланыВидовРасчета" Тогда
			
			СтрокаТипа = "ПланВидовРасчетаСсылка.";
			
		ИначеЕсли  ТипОбъекта = "ПланыВидовХарактеристик" Тогда
			
			СтрокаТипа = "ПланВидовХарактеристикСсылка.";
			
		ИначеЕсли  ТипОбъекта = "БизнесПроцессы" Тогда
			
			СтрокаТипа = "БизнесПроцессСсылка.";
			
		ИначеЕсли  ТипОбъекта = "Задачи" Тогда
			
			СтрокаТипа = "ЗадачаСсылка.";
			
		КонецЕсли;
		
		Если ОбрабатыватьТЧ = Ложь 
			или ТипЗнч(Элементобъектов.Значение) <> Тип("Структура") Тогда
			
			МассивТиповСсылка.Добавить(Тип(СтрокаТипа + Элементобъектов.Значение));
			
		Иначе
			
			МассивТиповСсылка.Добавить(Тип(СтрокаТипа + Элементобъектов.Значение.ИмяОбъекта));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбрабатыватьТЧ = Ложь Тогда
		
		Для Каждого ОбъектВыбора Из РезультатВыбора Цикл
			
			КоличествоРеквизитов = Метаданные[ТипОбъекта][ОбъектВыбора.Значение].Реквизиты.Количество();
			
			Для Каждого ОбъектМетаданных Из Метаданные[ТипОбъекта][ОбъектВыбора.Значение].Реквизиты Цикл
									
				НоваяСтрока = ТаблицаМетаданных.Добавить();
				
				НоваяСтрока.ИмяРеквизита = ОбъектМетаданных.Имя;
				НоваяСтрока.СинонимРеквизита = ОбъектМетаданных.Синоним;
				НоваяСтрока.ОписаниеТипов = ИсключитьНедопустимыеТипы(ОбъектМетаданных.Тип); 
				НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение;
						
				
			КонецЦикла;
			
			Для Каждого ОбъектМетаданных Из Метаданные[ТипОбъекта][ОбъектВыбора.Значение].СтандартныеРеквизиты Цикл
				
				Если ОбъектМетаданных.Имя = "Предопределенный" 
					Или ОбъектМетаданных.Имя = "ЭтоГруппа" 
					Или ОбъектМетаданных.Имя = "Ссылка" Тогда
					
					Продолжить;
					
				Иначе 
					

					НоваяСтрока = ТаблицаМетаданных.Добавить();
					
					НоваяСтрока.ИмяРеквизита = ОбъектМетаданных.Имя;
					НоваяСтрока.СинонимРеквизита = ОбъектМетаданных.Синоним;
					НоваяСтрока.ОписаниеТипов = ИсключитьНедопустимыеТипы(ОбъектМетаданных.Тип); 
					НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение;
								
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого ОбъектМетаданных Из Метаданные.ОбщиеРеквизиты Цикл
				Если ОбъектМетаданных.Состав.Найти(Метаданные[ТипОбъекта][ОбъектВыбора.Значение]).Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать Тогда
					НоваяСтрока = ТаблицаМетаданных.Добавить();
					
					НоваяСтрока.ИмяРеквизита = ОбъектМетаданных.Имя;
					НоваяСтрока.СинонимРеквизита = ОбъектМетаданных.Синоним;
					НоваяСтрока.ОписаниеТипов = ИсключитьНедопустимыеТипы(ОбъектМетаданных.Тип); 
					НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение;
				КонецЕсли;
			КонецЦикла;
			
			Если  ТипОбъекта = "Задачи" Тогда
				
				КоличествоРеквизитов = КоличествоРеквизитов +  Метаданные[ТипОбъекта][ОбъектВыбора.Значение].РеквизитыАдресации.Количество();
				
				Для каждого ОбъектМетаданных Из Метаданные[ТипОбъекта][ОбъектВыбора.Значение].РеквизитыАдресации Цикл
										
					НоваяСтрока = ТаблицаМетаданных.Добавить();
					
					НоваяСтрока.ИмяРеквизита = ОбъектМетаданных.Имя;
					НоваяСтрока.СинонимРеквизита = ОбъектМетаданных.Синоним;
					НоваяСтрока.ОписаниеТипов = ИсключитьНедопустимыеТипы(ОбъектМетаданных.Тип); 
					НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение;
									
				КонецЦикла;
				
			КонецЕсли;
					
			КоличествоРеквизитов = КоличествоРеквизитов + Метаданные[ТипОбъекта][ОбъектВыбора.Значение].СтандартныеРеквизиты.Количество();
			
			НоваяСтрока = ТаблицаОбщихРеквизитов.Добавить();
			
			НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение;
			НоваяСтрока.КоличествоРеквизитов = КоличествоРеквизитов;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого ОбъектВыбора Из РезультатВыбора Цикл
			
			Если ТипЗнч(ОбъектВыбора.Значение)  = Тип("Структура") Тогда
				
				КоличествоРеквизитов = Метаданные[ТипОбъекта][ОбъектВыбора.Значение.ИмяОбъекта].ТабличныеЧасти[ОбъектВыбора.Значение.ИмяТЧ].Реквизиты.Количество()
				+ Метаданные[ТипОбъекта][ОбъектВыбора.Значение.ИмяОбъекта].ТабличныеЧасти[ОбъектВыбора.Значение.ИмяТЧ].СтандартныеРеквизиты.Количество();
				
				Для Каждого ОбъектМетаданных Из Метаданные[ТипОбъекта][ОбъектВыбора.Значение.ИмяОбъекта].ТабличныеЧасти[ОбъектВыбора.Значение.ИмяТЧ].Реквизиты Цикл
					
					НоваяСтрока = ТаблицаМетаданных.Добавить();
					
					НоваяСтрока.ИмяРеквизита = ОбъектМетаданных.Имя;
					НоваяСтрока.СинонимРеквизита = ОбъектМетаданных.Синоним;
					НоваяСтрока.ОписаниеТипов = ОбъектМетаданных.Тип; 
					НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение.ИмяОбъекта + ОбъектВыбора.Значение.ИмяТЧ;
					
				КонецЦикла;
				
				Для Каждого ОбъектМетаданных Из Метаданные[ТипОбъекта][ОбъектВыбора.Значение.ИмяОбъекта].ТабличныеЧасти[ОбъектВыбора.Значение.ИмяТЧ].СтандартныеРеквизиты Цикл
					
					НоваяСтрока = ТаблицаМетаданных.Добавить();		
					НоваяСтрока.ИмяРеквизита = ОбъектМетаданных.Имя;
					НоваяСтрока.СинонимРеквизита = ОбъектМетаданных.Синоним;
					НоваяСтрока.ОписаниеТипов = ОбъектМетаданных.Тип; 
					НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение.ИмяОбъекта + ОбъектВыбора.Значение.ИмяТЧ;
					
				КонецЦикла;
				
				КС = Новый КвалификаторыСтроки(100);
				ОписаниеТиповС = Новый ОписаниеТипов(,,КС);
				
				НоваяСтрока = ТаблицаМетаданных.Добавить();		
				НоваяСтрока.ИмяРеквизита = "ИмяТЧ";
				НоваяСтрока.СинонимРеквизита = "Имя ТЧ";
				НоваяСтрока.ОписаниеТипов = ОписаниеТиповС; 
				НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение.ИмяОбъекта + ОбъектВыбора.Значение.ИмяТЧ;
				
				НоваяСтрока = ТаблицаОбщихРеквизитов.Добавить();
				
				НоваяСтрока.ОбъектМетаданных = ОбъектВыбора.Значение.ИмяОбъекта +  ОбъектВыбора.Значение.ИмяТЧ;
				НоваяСтрока.КоличествоРеквизитов = КоличествоРеквизитов;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаОбщихРеквизитов.Сортировать("КоличествоРеквизитов возр");
	
	Если ТаблицаОбщихРеквизитов.Количество() > 1 Тогда
		
		ИсключитьУникальныеРеквизиты(ТаблицаОбщихРеквизитов);
		
	КонецЕсли;	
	
	ОписаниеТиповСсылка = Новый ОписаниеТипов(МассивТиповСсылка);
	
	НоваяСтрока = ТаблицаМетаданных.Добавить();
	
	НоваяСтрока.ИмяРеквизита = "Ссылка";
	НоваяСтрока.СинонимРеквизита = "Ссылка";
	НоваяСтрока.ОписаниеТипов = ОписаниеТиповСсылка;
	
	ТаблицаМетаданных.Сортировать("ИмяРеквизита возр");
	
	Для Каждого ЭлементРеквизит Из ТаблицаМетаданных Цикл
		
		НовоеПолеНабораДанных = НовыйНаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПолеНабораДанных.Заголовок = ЭлементРеквизит.СинонимРеквизита;
		НовоеПолеНабораДанных.Поле = ЭлементРеквизит.ИмяРеквизита;
		НовоеПолеНабораДанных.ТипЗначения = ЭлементРеквизит.ОписаниеТипов;
		НовоеПолеНабораДанных.ПутьКДанным = ЭлементРеквизит.ИмяРеквизита;
		
		Если ЭлементРеквизит.ИмяРеквизита = "ИмяТЧ" Тогда
			
			  НовоеПолеНабораДанных.ОграничениеИспользования.Условие = Истина;
			  НовоеПолеНабораДанных.ОграничениеИспользования.Порядок = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НастройкиКомпоновки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	НоваяГруппировка = НастройкиКомпоновки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	НоваяГруппировка.Использование = Истина;
	
	Для Каждого ЭлементРеквизит Из ТаблицаМетаданных Цикл
		
		НовоеПолеКомпоновкиДанных = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		НовоеПолеКомпоновкиДанных.Использование = Истина;
		НовоеПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных(ЭлементРеквизит.ИмяРеквизита);
		
		ВыбранноеПоле = НоваяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Использование = Истина;
		ВыбранноеПоле.Заголовок = ЭлементРеквизит.СинонимРеквизита;
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(ЭлементРеквизит.ИмяРеквизита);
		
	КонецЦикла;
	
	АдресСКД = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных,ЭтаФорма.УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД);
	
	Объект.Компоновщик.Инициализировать(ИсточникНастроек);
	
	Объект.Компоновщик.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	Если ТипОбъекта = "Документы" Тогда
		
		НовоеПолеСортировки = Объект.Компоновщик.Настройки.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		НовоеПолеСортировки.Поле = Новый ПолеКомпоновкиДанных("Дата");	
		
	ИначеЕсли ТипОбъекта = "ПланыВидовХарактеристик" Тогда
		
		ИнициализироватьДопОтборХарактеристик();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьДопОтборХарактеристик()
	
	ОписаниеТипов = Новый ОписаниеТипов;
	
	Для Каждого ВыбраннаяХарактеристика Из РезультатВыбора Цикл
		
		ТипыЗначенияХарактеристики = Метаданные.ПланыВидовХарактеристик[ВыбраннаяХарактеристика.Значение].Тип;
		ОписаниеТипов = Новый ОписаниеТипов(ОписаниеТипов, ТипыЗначенияХарактеристики.Типы());
		
	КонецЦикла;
	
	ТипЗначенияХарактеристики = Новый ОписаниеТипов(ОписаниеТипов);
		
КонецПроцедуры

&НаСервере
Процедура ИсключитьУникальныеРеквизиты(ТаблицаОбщихРеквизитов)
	
	Эталон = ТаблицаОбщихРеквизитов.Получить(0).ОбъектМетаданных;
	
	ТаблицаРезультат = ТаблицаМетаданных.Выгрузить();
	
	Отбор = Новый Структура;
	Отбор.Вставить("ОбъектМетаданных", Эталон);	
	
	ТаблицаЭталонныхРеквизитов = ТаблицаРезультат.Скопировать(Отбор);
	ТаблицаРезультат.Очистить();
	
	Для Каждого СтрокаЭталон Из ТаблицаЭталонныхРеквизитов Цикл
		
		ОтборПоЭталону = Новый Структура;
		ОтборПоЭталону.Вставить("ИмяРеквизита", СтрокаЭталон.ИмяРеквизита);
		
		НайденныеСтроки = ТаблицаМетаданных.НайтиСтроки(ОтборПоЭталону); 
		
		Если НайденныеСтроки.Количество() = ТаблицаОбщихРеквизитов.Количество() Тогда
			
			НоваяСтрока = ТаблицаРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЭталон);
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	ТаблицаМетаданных.Очистить();
	ТаблицаМетаданных.Загрузить(ТаблицаРезультат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отобрать(Команда)
		
	Для Каждого КолонкаТаблицыФормы Из Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы Цикл
		
		Если КолонкаТаблицыФормы.Видимость = Истина Тогда
			
			Если КэшВидимыхКолонок.НайтиПоЗначению(КолонкаТаблицыФормы.Имя) = Неопределено тогда
				
				КэшВидимыхКолонок.Добавить(КолонкаТаблицыФормы.Имя);
			  
		  	КонецЕсли;	
		  
		КонецЕсли;
		
	КонецЦикла;
	
	
	Если ТаблицаВидыОбъектов.Количество() > 0 Тогда
		
		ОтобратьОбъектыСервер();
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нужно выбрать хотя бы один объект метаданных";
		Сообщение.Поле = "ТаблицаВидыОбъектов";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	ВидимостьЭлементов();
	
	ОчиститьУО();
	
	УстановитьНовоеДействие();
	
КонецПроцедуры

&НаСервере
Процедура ОтобратьОбъектыСервер()
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСКД);
	РезультатОтбора = Новый ТаблицаЗначений;
	
	ТаблицаКопия = ТаблицаМетаданных.Выгрузить();
	ТаблицаКопия.Свернуть("ИмяРеквизита, СинонимРеквизита, ОписаниеТипов");
	
	Для Каждого ОбъектМетаданных Из ТаблицаКопия Цикл
		
		НоваяКолонка = РезультатОтбора.Колонки.Добавить(ОбъектМетаданных.ИмяРеквизита, ОбъектМетаданных.ОписаниеТипов, ОбъектМетаданных.СинонимРеквизита);
		
	КонецЦикла;
	
	ДобавитьУдалитьРеквизиты(ТаблицаКопия);
	
	Если ОбрабатыватьТЧ = Ложь Тогда
		
		Для Каждого ОтобранныйОбъект Из РезультатВыбора Цикл
			
			Если ТипОбъекта = "Справочники" Тогда
				
				Если Метаданные.Справочники[ОтобранныйОбъект.Значение].Иерархический И Метаданные.Справочники[ОтобранныйОбъект.Значение].ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
				    Запрос = Новый Запрос(" 
				    |ВЫБРАТЬ 
				    |	* 
				    |ИЗ 
				    |	Справочник." + ОтобранныйОбъект.Значение + " 
				    |ГДЕ
					|	ЭтоГруппа = Ложь
					|");
					
					Выборка = Запрос.Выполнить().Выбрать();
				Иначе
					Выборка = Справочники[ОтобранныйОбъект.Значение].Выбрать();
				КонецЕсли;
				
			ИначеЕсли ТипОбъекта = "Документы" Тогда 
				
				Выборка = Документы[ОтобранныйОбъект.Значение].Выбрать();
				
			ИначеЕсли ТипОбъекта = "ПланыВидовРасчета" Тогда
				
				Выборка = ПланыВидовРасчета[ОтобранныйОбъект.Значение].Выбрать();
				
			ИначеЕсли ТипОбъекта = "ПланыВидовХарактеристик" Тогда
				
				Выборка = ПланыВидовХарактеристик[ОтобранныйОбъект.Значение].Выбрать();	 
				
			ИначеЕсли ТипОбъекта = "БизнесПроцессы" Тогда 
				
				Выборка = БизнесПроцессы[ОтобранныйОбъект.Значение].Выбрать();
				
			ИначеЕсли ТипОбъекта = "Задачи" Тогда	 
				
				Выборка = Задачи[ОтобранныйОбъект.Значение].Выбрать();
				
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				
				НоваяСтрока = РезультатОтбора.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка); 
				
			КонецЦикла;
			
		КонецЦикла;
		
		
	Иначе 
		
		СтрокаПолейЗапроса = "";
		КоличествоПолей = ТаблицаМетаданных.Количество();
		Счетчик = 1;
		
		Для Каждого СтрокаТМ Из ТаблицаМетаданных Цикл
			
			Если СтрокаТМ.ИмяРеквизита <> "ИмяТЧ" Тогда
				
				СтрокаПолейЗапроса = СтрокаПолейЗапроса + СтрокаТМ.ИмяРеквизита;
				
				Если Счетчик <> КоличествоПолей Тогда
					
					СтрокаПолейЗапроса = СтрокаПолейЗапроса + ", ";
					
				КонецЕсли;
				
			КонецЕсли;
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
		СтрокаПолейЗапроса = СтрокаПолейЗапроса +   ",""СТРОКАИМЯТЧ"" Как ИмяТЧ"; 
		
		ШаблонЗапроса = "ВЫБРАТЬ
		|	ОбъектСиноним." + СтрокаПолейЗапроса + " ИЗ ТИПОбъекта.ИмяОбъекта.ИмяТаблЧасти КАК ОбъектСиноним";
		
		Если ТипОбъекта = "Справочники" Тогда
			
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ТИПОбъекта", "Справочник");
			
		ИначеЕсли ТипОбъекта = "Документы" Тогда
			
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ТИПОбъекта", "Документ");
			
		ИначеЕсли ТипОбъекта = "ПланыВидовРасчета" Тогда	
			
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ТИПОбъекта", "ПланВидовРасчета");
			
		ИначеЕсли ТипОбъекта = "ПланыВидовХарактеристик" Тогда	
			
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ТИПОбъекта", "ПланВидовХарактеристик"); 
			
		ИначеЕсли ТипОбъекта = "БизнесПроцессы" Тогда	
			
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ТИПОбъекта", "БизнесПроцесс"); 
			
		ИначеЕсли ТипОбъекта = "Задачи" Тогда	
			
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "ТИПОбъекта", "Задача"); 
			
		КонецЕсли;				
		
		Счетчик = 1;
		КоличесвтоТЧ = РезультатВыбора.Количество();
		
		ТекстЗапроса = "";
		
		Для Каждого ОтобранныйОбъект Из РезультатВыбора Цикл 
			
			ТекстЗапросаПоОбъекту = СтрЗаменить(ШаблонЗапроса, "ИмяОбъекта", ОтобранныйОбъект.Значение.ИмяОбъекта);
			ТекстЗапросаПоОбъекту = СтрЗаменить(ТекстЗапросаПоОбъекту, "ИмяТаблЧасти", ОтобранныйОбъект.Значение.ИмяТЧ);
			ТекстЗапросаПоОбъекту = СтрЗаменить(ТекстЗапросаПоОбъекту, "СТРОКАИМЯТЧ", ОтобранныйОбъект.Значение.ИмяТЧ);
			
			ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоОбъекту;
			
			Если Счетчик <> КоличесвтоТЧ Тогда
				
				ТекстЗапроса = ТекстЗапроса + " Объединить все ";
				
			КонецЕсли;
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = РезультатОтбора.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;	
		
	КонецЕсли;
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("Основной",РезультатОтбора);
	
	НастройкиКомпоновки = Объект.Компоновщик.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
	НастройкиКомпоновки,,,
	Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;	
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,ВнешниеНаборыДанных);
	
	ТаблицаВывода = ТаблицаРезультатОтбор.Выгрузить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТаблицаРезультат = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ТаблицаРезультат.Колонки.Добавить("Использовать");
	ТаблицаРезультат.ЗаполнитьЗначения(Истина, "Использовать"); 
	
	ТаблицаРезультатОтбор.Очистить();
		
	Если ТипОбъекта = "ПланыВидовХарактеристик"
		И ОтборПоТипуДляХарактеристик Тогда
		
		Для Каждого СтрокаТаблицыРезультат Из ТаблицаРезультат Цикл
			
			Для Каждого ТипыОтбора Из ТипЗначенияХарактеристики.Типы() Цикл
				
				Если РежимОтбораХарактеристик = "По выбранным значениям" Тогда									
				
				Если СтрокаТаблицыРезультат.ТипЗначения.СодержитТип(ТипыОтбора) Тогда
					
					НоваяСтрока = ТаблицаРезультатОтбор.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыРезультат);
				
				КонецЕсли;
				
				ИначеЕсли РежимОтбораХарактеристик = "По доступным значениям" Тогда

				       Если СтрокаТаблицыРезультат.Ссылка.Метаданные().Тип.СодержитТип(ТипыОтбора) Тогда
						   
						   НоваяСтрока = ТаблицаРезультатОтбор.Добавить();
						   ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыРезультат);

					   КонецЕсли;					   
				
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		
		ТаблицаРезультатОтбор.Загрузить(ТаблицаРезультат);
		
	КонецЕсли;
		
	ИнициализироватьДействия();
	
КонецПроцедуры

&НаКлиенте
Процедура РекурсивныйОбходОтбора(ЭлементыОтбора)
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ЭлементОтбора) <> Тип("ГруппаЭлементовОтбораКомпоновкиДанных") 
			И ЭлементОтбора.Использование = Истина Тогда
			
			Колонка = Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти(Строка(ЭлементОтбора.ЛевоеЗначение));
			
			Если Колонка <> Неопределено Тогда
				
				Колонка.Видимость = Истина;
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ЭлементОтбора) <> Тип("ГруппаЭлементовОтбораКомпоновкиДанных") 
			И ЭлементОтбора.Использование = Истина Тогда
			
			РекурсивныйОбходОтбора(ЭлементОтбора.Элементы);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьЭлементов()
		
	Для Каждого Колонка Из Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы Цикл
		
		Если Колонка.Имя <> "Ссылка" 
			И Колонка.Имя <> "НомерСтроки"
			И Колонка.Имя <> "ТаблицаРезультатОтборОбработанУспешно"
			И Колонка.Имя <> "ИмяТЧ" Тогда
			
			Колонка.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	РекурсивныйОбходОтбора(Объект.Компоновщик.Настройки.Отбор.Элементы);
	
	Если ВыберитеДействие = "ИзменитьПометкаУдаления" Тогда
		
		Колонка = Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти("ПометкаУдаления");
		
		Если Колонка <> Неопределено Тогда
			
			Колонка.Видимость = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ВыберитеДействие = "ИзменитьПроведение" Тогда
		
		Колонка = Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти("Проведен");
		
		Если Колонка <> Неопределено Тогда
			
			Колонка.Видимость = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ВыберитеДействие = "ВыполнитьЗадачу"  Тогда
		
		Колонка = Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти("Выполнена");
		
		Если Колонка <> Неопределено Тогда
			
			Колонка.Видимость = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ВыберитеДействие = "Старт"  Тогда
		
		Колонка = Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти("Стартован");
		
		Если Колонка <> Неопределено Тогда
			
			Колонка.Видимость = Истина;
			
		КонецЕсли;
		
	Иначе
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяРеквизита", ВыберитеДействие);
		НайденноеЗначение = ТаблицаМетаданных.НайтиСтроки(Отбор);
		
		Если НайденноеЗначение.Количество() > 0 тогда
			
			НайденноеЗначение = НайденноеЗначение.ПОлучить(0).ИмяРеквизита;
			
			Колонка = Элементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти(НайденноеЗначение);
			
			Если Колонка <> Неопределено Тогда
				
				Колонка.Видимость = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

	Если КэшВидимыхКолонок.Количество() > 0 Тогда
		
		Для Каждого ВидимаяКолонка Из КэшВидимыхКолонок Цикл
			
			КолонкаТаблицыФормы =
			Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти(ВидимаяКолонка.Значение);
			
			Если КолонкаТаблицыФормы <> Неопределено Тогда
				
				КолонкаТаблицыФормы.Видимость = Истина;
				
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;
	
	Если ВыберитеДействие <> "ИзменитьРеквизиты" Тогда
		
		Элементы.ГруппаРедактироватьРеквизиты.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаРедактироватьРеквизиты.Видимость = Истина;

	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьУдалитьРеквизиты(ТаблицаКопия = Неопределено)
	
	РеквизитыНаУдаление = Новый Массив;
	ЭлементыНаУдаление = Новый Массив;
	
	Для Каждого ЭлементРеквизита Из Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы Цикл
		
		РеквизитыНаУдаление.Добавить(ЭлементРеквизита.ПутьКДанным);
		ЭлементыНаУдаление.Добавить(ЭлементРеквизита);
		
	КонецЦикла;
	
	Если РеквизитыНаУдаление.Количество() > 0 Тогда
		
		ЭтаФорма.ИзменитьРеквизиты(,РеквизитыНаУдаление);
		
		Для Каждого ЭлементНаУдаление Из ЭлементыНаУдаление Цикл
			
			Элементы.Удалить(ЭлементНаУдаление);
			
		КонецЦикла;
		
	КонецЕсли;
	
	
	МассивРеквизитов = Новый Массив;
	
	Если ТаблицаКопия <> Неопределено Тогда
		
		Для Каждого ЭлементРекзвизита Из ТаблицаКопия Цикл
			
			НовыйРеквизитФормы = Новый РеквизитФормы(ЭлементРекзвизита.ИмяРеквизита
			, ЭлементРекзвизита.ОписаниеТипов
			, "ТаблицаРезультатОтбор"
			, ЭлементРекзвизита.СинонимРеквизита);
			
			МассивРеквизитов.Добавить(НовыйРеквизитФормы);
			
		КонецЦикла;	
		
		ЭтаФорма.ИзменитьРеквизиты(МассивРеквизитов);
		
		Для Каждого ЭлементРеквизита Из ТаблицаКопия Цикл
			
			НовыйЭлементРодитель = Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно;
			НовыйЭлемент = Элементы.Добавить(ЭлементРеквизита.ИмяРеквизита, Тип("ПолеФормы"), НовыйЭлементРодитель);
			НовыйЭлемент.ПутьКДанным = "ТаблицаРезультатОтбор." + ЭлементРеквизита.ИмяРеквизита;
			НовыйЭлемент.Заголовок = ?(СтрДлина(ЭлементРеквизита.СинонимРеквизита) = 0, ЭлементРеквизита.ИмяРеквизита, ЭлементРеквизита.СинонимРеквизита);
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.ТолькоПросмотр = Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИсключитьНедопустимыеТипы(ТипРеквизита) 
	
	    МассивНедопустимых = ПолучитьНедопустимыеТипы();
		
		МассивИсключаемыхТипов = Новый Массив;
		
		Для Каждого ЭлементНедопустимый Из МассивНедопустимых Цикл
			
			Если ТипРеквизита.СодержитТип(ЭлементНедопустимый) Тогда
				
				  МассивИсключаемыхТипов.Добавить(ЭлементНедопустимый);
				
			КонецЕсли;
			
		КонецЦикла;

		Если МассивИсключаемыхТипов.Количество() = 0 Тогда
			
			ОписаниеРеквизита = ТипРеквизита;
			
		Иначе
			
			ОписаниеРеквизита = Новый ОписаниеТипов(ТипРеквизита, , МассивИсключаемыхТипов);
			
		КонецЕсли;
		
		Возврат ОписаниеРеквизита;
		
КонецФункции

&НаСервереБезКонтекста
Функция  ПолучитьНедопустимыеТипы()
			
	МассивНедопустимыхТипов = Новый Массив;
	МассивНедопустимыхТипов.Добавить(Тип("ХранилищеЗначения"));
		
	Возврат МассивНедопустимыхТипов;
		
КонецФункции

&НаКлиенте
Процедура РедактироватьСКД(Команда)
	
	#Если ТолстыйКлиентУправляемоеПриложение тогда
		СКД = ПолучитьИзВременногоХранилища(АдресСКД);
		КонструкторСКД = Новый КонструкторСхемыКомпоновкиДанных;
		КонструкторСКД.УстановитьСхему(СКД);
		КонструкторСКД.Редактировать(ЭтаФорма);
		
	#Иначе 
		
		Сообщить("Функция работает только в режиме ТолстыйКлиентУправляемоеПриложение");
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВидыОбъектовПослеУдаления(Элемент)
	
	ТаблицаРезультатОтбор.Очистить();
	ДобавитьУдалитьРеквизиты();
	ИнициализироватьСКД();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыберитеДействиеПриИзменении(Элемент)
	
	УстановитьНовоеДействие();
	ВидимостьЭлементов();
	
	Если ВыберитеДействие = "ИзменитьРеквизиты" Тогда
		
		ПодготовитьДействиеИзменитьРеквизиты();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДействиеИзменитьРеквизиты()
	
	ПодготовитьПараметрыДопРеквизиты();
	
	Если ВыберитеДействие = "ИзменитьРеквизиты" Тогда
				
		Элементы.ГруппаРедактироватьРеквизиты.Видимость = Истина
		
	Иначе
		
		Элементы.ГруппаРедактироватьРеквизиты.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура  ПодготовитьПараметрыДопРеквизиты()
		
	КэшТаблицаРеквизиты = ТаблицаРеквизиты.Выгрузить();
	ТаблицаРеквизиты.Очистить();

	Для Каждого ЭлементМетаданных Из ТаблицаМетаданных Цикл
			
			Если ЭлементМетаданных.ИмяРеквизита <> "Ссылка" 
				И ЭлементМетаданных.ИмяРеквизита <> "ИмяТЧ"  
				И ЭлементМетаданных.ИмяРеквизита <> "НомерСтроки" 
				И ЭлементМетаданных.ИмяРеквизита <> "Предопределенный" Тогда
				
				НоваяСтрока = ТаблицаРеквизиты.Добавить();				
				
				НоваяСтрока.ИмяРеквизита           		 =  ЭлементМетаданных.ИмяРеквизита;
				
				Если СтрДлина(ЭлементМетаданных.СинонимРеквизита) = 0 Тогда
					
					НоваяСтрока.СинонимРеквизита 		     =  ЭлементМетаданных.ИмяРеквизита;
					
				Иначе
					
	  				НоваяСтрока.СинонимРеквизита 		     =  ЭлементМетаданных.СинонимРеквизита;
					
				КонецЕсли;
			
	  		 	НоваяСтрока.ДоступныеТипыЗначений =  ЭлементМетаданных.ОписаниеТипов;
	 		  	НоваяСтрока.Изменять                		      =  Ложь;
 
				НайденнаяСтрокаКэша = КэшТаблицаРеквизиты.Найти(НоваяСтрока.ИмяРеквизита, "ИмяРеквизита");
				
				Если  НайденнаяСтрокаКэша <> Неопределено Тогда
					
					Если  НоваяСтрока.ДоступныеТипыЗначений.СодержитТип(ТипЗнч(НайденнаяСтрокаКэша.НовоеЗначение)) Тогда
						
						НоваяСтрока.НовоеЗначение = НайденнаяСтрокаКэша.НовоеЗначение;
						НоваяСтрока.Изменять = НайденнаяСтрокаКэша.Изменять;
						
					КонецЕсли;
					
				КонецЕсли;
				
	 		  	НоваяСтрока.ДоступныеТипыЗначений = ЭлементМетаданных.ОписаниеТипов;
				
			КонецЕсли;
			
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьНовоеДействие()
	
	УдалитьЭлементНовоеЗначение();
	МожноСтартовать = Ложь;
	
	МассивНовыхРекзвизитов = Новый Массив;
	
	Если ВыберитеДействие = "ИзменитьПометкаУдаления" 
		Или ВыберитеДействие = "ИзменитьПроведение" Тогда
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("Булево"));
		
		ОписаниеТиповБулево = Новый ОписаниеТипов(МассивТипов);
		
		НовыйРекзвизитФормы = Новый РеквизитФормы("НовоеЗначение", ОписаниеТиповБулево, , "Новое значение");
		
		МассивНовыхРекзвизитов.Добавить(НовыйРекзвизитФормы);
		
		ЭтаФорма.ИзменитьРеквизиты(МассивНовыхРекзвизитов);
		
		НовыйЭлементРодитель = Элементы.ГруппаДействие.ПодчиненныеЭлементы.ГруппаВыберитеДействиеОсновное.ПодчиненныеЭлементы.ГруппаНовоеЗначение;
		НовыйЭлемент = Элементы.Добавить("НовоеЗначение", Тип("ПолеФормы"), НовыйЭлементРодитель);
		НовыйЭлемент.ПутьКДанным = "НовоеЗначение";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		
	ИначеЕсли ВыберитеДействие = "ВыполнитьЗадачу"
		Или (ВыберитеДействие = "Старт" 
		И ОбрабатыватьТЧ = Истина) Тогда 
		
		//Никакого выбора действия нет
		
	ИначеЕсли ВыберитеДействие = "Старт" 
		И ОбрабатыватьТЧ = Ложь Тогда
		
		БизнесПроцессПример = РезультатВыбора.Получить(0).Значение;
		
		//Если выбран 1 Бизнес-Процесс и 	
		//то можно предоставить 
		//пользователю выбор точки старта                      	
		
		Если РезультатВыбора.Количество() = 1 Тогда
			
			КартаМаршрута = БизнесПроцессы[БизнесПроцессПример] .ПолучитьКартуМаршрута();
			
			СписокТочек = Новый СписокЗначений;
			
			Счетчик = 0;
			
			Для Каждого ЭлементСхемы Из КартаМаршрута.ЭлементыГрафическойСхемы  Цикл
				
				Если ТипЗнч(ЭлементСхемы) = Тип("ЭлементГрафическойСхемыСтарт") Тогда
					
					СписокТочек.Добавить(Счетчик, ЭлементСхемы.Наименование);
					
				КонецЕсли;
				
				Счетчик = Счетчик + 1;
				
			КонецЦикла;
			
			Если СписокТочек.Количество()  > 0 Тогда
				
				МожноСтартовать = Истина;
				
				МассивТС = Новый Массив;
				МассивТС.Добавить(Тип("Строка"));
				
				ОписаниеТиповТС = Новый ОписаниеТипов(МассивТС);
				НовыйРекзвизитФормы = Новый РеквизитФормы("НовоеЗначение", ОписаниеТиповТС, , "Точка старта");
				
				МассивНовыхРекзвизитов.Добавить(НовыйРекзвизитФормы);
				
				ЭтаФорма.ИзменитьРеквизиты(МассивНовыхРекзвизитов);
				
				НовыйЭлементРодитель = Элементы.ГруппаДействие.ПодчиненныеЭлементы.ГруппаВыберитеДействиеОсновное.ПодчиненныеЭлементы.ГруппаНовоеЗначение;
				НовыйЭлемент = Элементы.Добавить("НовоеЗначение", Тип("ПолеФормы"), НовыйЭлементРодитель);
				НовыйЭлемент.ПутьКДанным = "НовоеЗначение";
				НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
				НовыйЭлемент.КнопкаСпискаВыбора = Истина;
				НовыйЭлемент.РедактированиеТекста = Ложь;
				
				Для Каждого ТочкаСтартаБП Из СписокТочек Цикл
					
					НовыйЭлемент.СписокВыбора.Добавить(ТочкаСтартаБП.Значение, ТочкаСтартаБП.Представление);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяРеквизита", ВыберитеДействие);
		НайденноеЗначение = ТаблицаМетаданных.НайтиСтроки(Отбор);
		
		Если НайденноеЗначение.Количество() > 0 тогда
			
			НайденноеЗначение = НайденноеЗначение.Получить(0);
			
			НовыйРекзвизитФормы = Новый РеквизитФормы("НовоеЗначение", НайденноеЗначение.ОписаниеТипов, , "Новое значение");
			
			МассивНовыхРекзвизитов.Добавить(НовыйРекзвизитФормы);
			
			ЭтаФорма.ИзменитьРеквизиты(МассивНовыхРекзвизитов);
			
			НовыйЭлементРодитель = Элементы.ГруппаДействие.ПодчиненныеЭлементы.ГруппаВыберитеДействиеОсновное.ПодчиненныеЭлементы.ГруппаНовоеЗначение;
			НовыйЭлемент = Элементы.Добавить("НовоеЗначение", Тип("ПолеФормы"), НовыйЭлементРодитель);
			НовыйЭлемент.ПутьКДанным = "НовоеЗначение";
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
			
			Если ВыберитеДействие = "ТипЗначения" 
				И ТипОбъекта = "ПланыВидовХарактеристик" Тогда
				
				НовыйЭлемент.УстановитьДействие("НачалоВыбора", "ТипЗначенияХарактеристикНачалоВыбора");
				
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура  ТипЗначенияХарактеристикНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивДоступныхТипов = ПолучитьДоступныеДляУстановкиТипыЗначений();
	
	П = Новый Структура;
	П.Вставить("МассивТипов", ЭтаФорма["НовоеЗначение"].Типы());
	П.Вставить("ДоступныеТипы", Новый ОписаниеТипов(МассивДоступныхТипов));
		
	Результат = 	ОткрытьФормуМодально("Обработка.ГрупповаяОбработкаСправочниковИДокументов.Форма.ФормаВыбораТиповХарактеристики", П);
	
	Если Результат <> Неопределено Тогда
		
		МассивТипов = Новый Массив;
		
		Для Каждого ВыбранныйТип Из Результат Цикл
			
			Если ВыбранныйТип.Пометка Тогда
				
				МассивТипов.Добавить(ВыбранныйТип.Значение);
				
			КонецЕсли;
			
			
			
		КонецЦикла;
		
		ЭтаФорма["НовоеЗначение"] = Новый ОписаниеТипов(МассивТипов);
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДоступныеДляУстановкиТипыЗначений()
	  //itl
	ТаблицаТипов = Новый ТаблицаЗначений;
	ТаблицаТипов.Колонки.Добавить("МассивТипов");
	ТаблицаТипов.Колонки.Добавить("КоличествоРазличных");
	
	Для Каждого ВыбраннаяХарактеристика Из РезультатВыбора Цикл
		
		ТипыЗначенияХарактеристики = Метаданные.ПланыВидовХарактеристик[ВыбраннаяХарактеристика.Значение].Тип;
		
		НоваяСтрока = ТаблицаТипов.Добавить();
		НоваяСтрока.МассивТипов = ТипыЗначенияХарактеристики.Типы();
		НоваяСтрока.КоличествоРазличных = НоваяСтрока.МассивТипов.Количество();
		
	КонецЦикла;
	
	ТаблицаТипов.Сортировать("КоличествоРазличных Возр");
	
	МассивДоступныхТипов = Новый Массив;
	
	СтрокаЭталон = ТаблицаТипов.Получить(0);
	
	Для Каждого ЭлементЭталона Из СтрокаЭталон.МассивТипов Цикл
		
		ВходитВоВсе = Истина;

		Если ТаблицаТипов.Количество() > 1 Тогда
						
			Для Х = 1 По ТаблицаТипов.Количество() - 1 Цикл
								
				СтрокаСравнение = ТаблицаТипов.Получить(Х);
				
				ОписаниеТиповСравнение = Новый ОписаниеТипов(СтрокаСравнение.МассивТипов);
				
				Если ОписаниеТиповСравнение.СодержитТип(ЭлементЭталона) = Ложь Тогда
					
					ВходитВоВсе = Ложь;
					Прервать;
					
				КонецЕсли;
					
			КонецЦикла;	
			
		КонецЕсли;
		
		Если ВходитВоВсе Тогда
			
			 МассивДоступныхТипов.Добавить(ЭлементЭталона);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивДоступныхТипов;
		
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	Если СтрДлина(ВыберитеДействие) = 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите действие!";
		Сообщение.Поле = "ВыберитеДействие";
		
		Сообщение.Сообщить();
		
	Иначе 
				
				
		Состояние("Выполняется: " + ВыберитеДействие);
		ВыполнитьВыбранноеДействие();
		Состояние("Выполнено: " + ВыберитеДействие);
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьВыбранноеДействие()
	
	ОчиститьУО();
	
	ТаблицаОбработки = ТаблицаРезультатОтбор.Выгрузить();
	
	Если Объект.ВыполнятьВТранзакции Тогда
		
		НачатьТранзакцию();	
		
	КонецЕсли;
	
	Для Каждого ОтобранныйЭлемент Из ТаблицаРезультатОтбор Цикл
		
		Если ОтобранныйЭлемент.Использовать Тогда
						
			ОбрабатываемыйОбъект = ОтобранныйЭлемент.Ссылка.ПолучитьОбъект();
			ОтобранныйЭлемент.ОбработанУспешно = "Да";

			Если Объект.ВыполнятьВРежимеЗагрузки Тогда
				
				ОбрабатываемыйОбъект.ОбменДанными.Загрузка = Истина;
				
			КонецЕсли;
			
			Попытка
				
				ВыполнитьДействиеСтрокаТЧ(ОбрабатываемыйОбъект,  ВыберитеДействие, ОтобранныйЭлемент);
				
			Исключение
				
				Если ТранзакцияАктивна() Тогда
					
					ОтменитьТранзакцию();
					
				КонецЕсли;
				
			 	ОтобранныйЭлемент.ОбработанУспешно = "Нет";
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ОписаниеОшибки();
				Сообщение.УстановитьДанные(ОбрабатываемыйОбъект);
				Сообщение.Сообщить();
				
				Если Объект.ВыполнятьВТранзакции Или
					(Не Объект.ВыполнятьВТранзакции И Не Объект.НеПрерыватьВыполнениеПриОшибке) Тогда
					
					Прервать;
					
				КонецЕсли;
			
			КонецПопытки;
			
		Иначе
			
			  ОтобранныйЭлемент.ОбработанУспешно = "НеОбрабатывался";
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Объект.ВыполнятьВТранзакции 
		И ТранзакцияАктивна() Тогда
		
		ЗафиксироватьТранзакцию();
		
	КонецЕсли;
	
	ОбновитьПослеВыполнения();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействиеСтрокаТЧ(ОбрабатываемыйОбъект, ВыбранноеДействие, ОтобранныйЭлемент)
	
	Если ВыберитеДействие = "ИзменитьПометкаУдаления" Тогда
		
		ОбрабатываемыйОбъект.УстановитьПометкуУдаления(ЭтаФорма["НовоеЗначение"]);
		
	ИначеЕсли ВыберитеДействие = "ИзменитьПроведение" Тогда
		
		Если ЭтаФорма["НовоеЗначение"] Тогда
			
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			
		Иначе
			
			РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
			
		КонецЕсли;
		
		ОбрабатываемыйОбъект.Записать(РежимЗаписи);
		
	ИначеЕсли ВыберитеДействие = "ВыполнитьЗадачу" Тогда
		
		ОбрабатываемыйОбъект.ВыполнитьЗадачу(); 
		
	ИначеЕсли ВыберитеДействие = "Старт" Тогда
		
		ИндексТочкаСтарта = Неопределено;
		
		Если МожноСтартовать Тогда
			
			ИндексТочкаСтарта = ЭтаФорма["НовоеЗначение"];
			
		КонецЕсли;
		
		Если ИндексТочкаСтарта <> Неопределено 
			И СтрДлина(ИндексТочкаСтарта) > 0 Тогда
			
			КартаМ = ОбрабатываемыйОбъект.ПолучитьКартуМаршрута();
			ТочкаМ = КартаМ.ЭлементыГрафическойСхемы.Получить(Число(ИндексТочкаСтарта));
			
		КонецЕсли;
		
		Если ИндексТочкаСтарта = Неопределено Тогда
			
			ОбрабатываемыйОбъект.Старт(); 
			
		Иначе 
			
			ОбрабатываемыйОбъект.Старт(ТочкаМ);
			
		КонецЕсли;
		
	ИначеЕсли ВыберитеДействие = "ИзменитьРеквизиты" Тогда

		ВыполнитьДействиеИзменитьРеквизиты(ОбрабатываемыйОбъект, ОтобранныйЭлемент);
		
	Иначе
		
		ВыполнитьДействиеИзменитьРеквизит(ОбрабатываемыйОбъект, ОтобранныйЭлемент);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура  ВыполнитьДействиеИзменитьРеквизиты(ОбрабатываемыйОбъект, ОтобранныйЭлемент)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Изменять", Истина);
	
	ТаблицаИзменяемыеРеквизиты = ТаблицаРеквизиты.Выгрузить(ПараметрыОтбора);
	
	СтруктураЗаполнения = Новый Структура;
	
	Для Каждого ИзменяемыйРеквизит Из ТаблицаИзменяемыеРеквизиты Цикл
		
		СтруктураЗаполнения.Вставить(ИзменяемыйРеквизит.ИмяРеквизита, ИзменяемыйРеквизит.НовоеЗначение);
		
	КонецЦикла;
	
	Если ОбрабатыватьТЧ = Ложь Тогда
		
		ЗаполнитьЗначенияСвойств(ОбрабатываемыйОбъект, СтруктураЗаполнения);
		
	Иначе 
		
		НужнаяСтрока = ОбрабатываемыйОбъект[ОтобранныйЭлемент.ИмяТЧ].Получить(ОтобранныйЭлемент.НомерСтроки - 1);
		ЗаполнитьЗначенияСвойств(НужнаяСтрока, СтруктураЗаполнения);
			
	КонецЕсли;
	
	ОбрабатываемыйОбъект.Записать(); 
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействиеИзменитьРеквизит(ОбрабатываемыйОбъект, ОтобранныйЭлемент)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизита", ВыберитеДействие);
	НайденноеЗначение = ТаблицаМетаданных.НайтиСтроки(Отбор);
	
	Если НайденноеЗначение.Количество() > 0 тогда
		
		НайденноеЗначение = НайденноеЗначение.Получить(0);
		
		Если ОбрабатыватьТЧ = Ложь Тогда
			
			ОбрабатываемыйОбъект[ВыберитеДействие] = ЭтаФорма["НовоеЗначение"];
			
		Иначе
			
			НужнаяСтрока = ОбрабатываемыйОбъект[ОтобранныйЭлемент.ИмяТЧ].Получить(ОтобранныйЭлемент.НомерСтроки - 1);
			НужнаяСтрока[ВыберитеДействие] = ЭтаФорма["НовоеЗначение"];
			
		КонецЕсли;
		
		ОбрабатываемыйОбъект.Записать(); 
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьПослеВыполнения()
	
	Если ОбрабатыватьТЧ = Ложь Тогда
		
		Для Каждого ЭлементСписка Из ТаблицаРезультатОтбор Цикл
			
			ЗаполнитьЗначенияСвойств(ЭлементСписка, ЭлементСписка.Ссылка);
			
		КонецЦикла;
		
	Иначе 
		
		Для Каждого ЭлементСписка Из ТаблицаРезультатОтбор Цикл
			
			НужнаяСтрока = ЭлементСписка.Ссылка[ЭлементСписка.ИмяТЧ].Получить(ЭлементСписка.НомерСтроки - 1);
			ЗаполнитьЗначенияСвойств(ЭлементСписка, НужнаяСтрока);
			
		КонецЦикла;
		
	КонецЕсли;
	
	НовыйЭлементУО = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
	НовыйЭлементУО.Использование = Истина;
		
	НовыйЭлементОтбора = НовыйЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаРезультатОтбор.ОбработанУспешно");	
	НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НовыйЭлементОтбора.ПравоеЗначение = "Нет";
	
	НовыйЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(255,,));
	
	НовоеПолеОформление =  НовыйЭлементУО.Поля.Элементы.Добавить();
	НовоеПолеОформление.Поле = Новый ПолеКомпоновкиДанных("ТаблицаРезультатОтборИспользовать");
	
	Для Каждого  ДоступнаяКолонка Из Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(ДоступнаяКолонка) = Тип("ПолеФормы") Тогда
			
			НовоеПолеОформление =  НовыйЭлементУО.Поля.Элементы.Добавить();
			НовоеПолеОформление.Поле = Новый ПолеКомпоновкиДанных(ДоступнаяКолонка.Имя);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРезультатОтборВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаРезультатОтбор.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ОткрытьЗначение(ТекущиеДанные[Поле.Имя]);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьФлажки(Команда)
	
	УстановитьФлажки(Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлажки(Значение)
	
	Для Каждого СтрокаТО Из ТаблицаРезультатОтбор Цикл
		
		СтрокаТО.Использовать = Значение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСнятьФлажки(Команда)
	
	УстановитьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьКолонок(Команда)
	
	СписокЭлементов = Новый СписокЗначений;
	
	Для Каждого ЭлементУправления Из Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы Цикл
		
		Если ЭлементУправления.Имя <> "Ссылка" 
			И ЭлементУправления.Имя <> "НомерСтроки"
			И ЭлементУправления.Имя <> "ИмяТЧ" Тогда
			
			СписокЭлементов.Добавить(ЭлементУправления.Имя, ЭлементУправления.Заголовок, ЭлементУправления.Видимость);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТаблицаКолонок", СписокЭлементов);
	
	Форма = ПолучитьФорму("Обработка.ГрупповаяОбработкаСправочниковИДокументов.Форма.ФормаВидимостьКолонок", ПараметрыФормы);
	
	РезультатНастройки = Форма.ОткрытьМодально();
	
	Если РезультатНастройки <> Неопределено Тогда
		
		Для Каждого ЭлементСписка Из РезультатНастройки Цикл
			
			КолонкаИзменятьВидимость = Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти(ЭлементСписка.Значение);
			
			Если КолонкаИзменятьВидимость <> Неопределено Тогда
				
				КолонкаИзменятьВидимость.Видимость = ЭлементСписка.Пометка;
				
			КонецЕсли;			
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого КолонкаТаблицыФормы Из Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы Цикл
		
		Если КолонкаТаблицыФормы.Видимость = Истина Тогда
			
			Если КэшВидимыхКолонок.НайтиПоЗначению(КолонкаТаблицыФормы.Имя) = Неопределено тогда
				
				КэшВидимыхКолонок.Добавить(КолонкаТаблицыФормы.Имя);
			  
		  	КонецЕсли;	
		  
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбрабатыватьТЧПриИзменении(Элемент)
	
	ТаблицаВидыОбъектов.Очистить();
	ТаблицаРезультатОтбор.Очистить();
	ПерезаполнитьДеревоТаблиц(ОбрабатыватьТЧ);
	ЭтаФорма.ОбновитьОтображениеДанных();
	
	Если ВыберитеДействие = "ИзменитьРеквизиты" Тогда
		
		ПодготовитьДействиеИзменитьРеквизиты();
		
	КонецЕсли;

	Элементы.ДеревоТаблицГруппаСвернутьРазвернуть.Видимость = ОбрабатыватьТЧ;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	СохранитьДанныеВНастройках();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы = Ложь Тогда
		ПередЗакрытиемНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеВНастройках()
	
	Настройки = Новый Соответствие;
	ЗаполнитьНастройкиПриСохраненииНаСервере(Настройки);
		
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы,, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура  СериализоватьОтбор(пОтбор, ЗаписьXML) Экспорт 
	
	Для Каждого цЭлементОтбора Из пОтбор Цикл
		
		СериализаторXDTO.ЗаписатьXML(ЗаписьXML, цЭлементОтбора);
		
	КонецЦикла;
	
КонецПроцедуры // СериализоватьОтбор()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Настройки = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы);
	ВосстановитьНастройки(Настройки);	
	
КонецПроцедуры

&НаСервере
Процедура ДесериализоватьОтбор(пНастройки_Отбор, пОтбор)
	
	пОтбор.Элементы.Очистить();
	
	Если пНастройки_Отбор <> Неопределено Тогда
		
		ЧтениеXML = Новый ЧтениеXML();
		ЧтениеXML.УстановитьСтроку(пНастройки_Отбор);
		ЧтениеXML.ПерейтиКСодержимому();
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "Отборы" Тогда
			
			ЧтениеXML.Прочитать();
			
			Пока ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя <> "Отборы" Цикл
				
				ПолеОтбораXML = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
				ПолеОтбора = пОтбор.Элементы.Добавить(ТипЗнч(ПолеОтбораXML));
				СкопироватьЭлементыОтбораРекурсивно(ПолеОтбораXML, ПолеОтбора);
				//ПолеОтбора.Поле = ПолеОтбораXML.Поле;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ДесериализоватьОтбор(

&НаСервере
Процедура СкопироватьЭлементыОтбораРекурсивно(пОтборОткуда, пОтборКуда) Экспорт 
	
	ЗаполнитьЗначенияСвойств(пОтборКуда, пОтборОткуда);
	
	Если ТипЗнч(пОтборОткуда) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		
		Для Каждого ЭлементОтбора Из пОтборОткуда.Элементы Цикл
			
			НовыйЭлементОтбора = пОтборКуда.Элементы.Добавить(ТипЗнч(ЭлементОтбора));
			СкопироватьЭлементыОтбораРекурсивно(ЭлементОтбора, НовыйЭлементОтбора);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // СкопироватьЭлементыОтбораРекурсивно()

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	
	ТаблицаВидыОбъектов.Очистить();
	ТаблицаРезультатОтбор.Очистить();
	ВыберитеДействие = "";
	
	ПерезаполнитьДеревоТаблиц(ОбрабатыватьТЧ);
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДеревоТаблиц(ОбрабатыватьТЧКлиент = Неопределено)
	
	Если ТипОбъекта = "ПланыВидовХарактеристик" Тогда
		
		Элементы.ГруппаДопОтборХарактеристик.Видимость = Истина;
		
	Иначе
		
		Элементы.ГруппаДопОтборХарактеристик.Видимость = Ложь;
		
	КонецЕсли;	
	
	Если ОбрабатыватьТЧКлиент <> Неопределено Тогда
		
		ОбрабатыватьТЧ = ОбрабатыватьТЧКлиент;
		
	КонецЕсли;	
	
	ТаблицаОтмеченных = ТаблицаВидыОбъектов.Выгрузить();
	
	ДеревоМетаданных = РеквизитФормыВЗначение("ДеревоТаблиц");
	ДеревоМетаданных.Строки.Очистить();
	
	Для Каждого ЭлементМетаданных Из Метаданные[ТипОбъекта] Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяТаблицы", ЭлементМетаданных.Имя);
		
		НайденныеСтроки = ТаблицаОтмеченных.НайтиСтроки(Отбор);
		
		Если ОбрабатыватьТЧ 
			И ЭлементМетаданных.ТабличныеЧасти.Количество() = 0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока = ДеревоМетаданных.Строки.Добавить();
		
		НоваяСтрока.ПредставлениеТаблицы = ЭлементМетаданных.Синоним;
		НоваяСтрока.ИмяТаблицы			 = ЭлементМетаданных.Имя;
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			НоваяСтрока.Использовать = Истина;
			
		КонецЕсли;
		
		Если ОбрабатыватьТЧ Тогда
			
			Для Каждого МетаТабличнаяЧасть Из ЭлементМетаданных.ТабличныеЧасти Цикл 
				
				
				НоваяСтрокаПодчиненная = НоваяСтрока.Строки.Добавить();
				
				НоваяСтрокаПодчиненная.ПредставлениеТаблицы = МетаТабличнаяЧасть.Синоним;
				НоваяСтрокаПодчиненная.ИмяТаблицы			 = МетаТабличнаяЧасть.Имя;
				
				Отбор = Новый Структура;
				Отбор.Вставить("ИмяТаблицы", ЭлементМетаданных.Имя);
                Отбор.Вставить("ИмяТЧ", МетаТабличнаяЧасть.Имя);
				
				НайденныеСтроки = ТаблицаОтмеченных.НайтиСтроки(Отбор);
				
				Если НайденныеСтроки.Количество() > 0 Тогда
			
					НоваяСтрокаПодчиненная.Использовать = Истина;
			
				КонецЕсли;
		
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоМетаданных, "ДеревоТаблиц");
	ОбрабатыватьТЧ = ОбрабатыватьТЧ;

	
КонецПроцедуры


&НаКлиенте
Процедура Настройки(Команда)
	
	П = Новый Структура;
	П.Вставить("ВыполнятьВТранзакции", Объект.ВыполнятьВТранзакции);
	П.Вставить("ВыполнятьВРежимеЗагрузки", Объект.ВыполнятьВРежимеЗагрузки);
	П.Вставить("НеПрерыватьВыполнениеПриОшибке", Объект.НеПрерыватьВыполнениеПриОшибке);
	
	Результат = ОткрытьФормуМодально("Обработка.ГрупповаяОбработкаСправочниковИДокументов.Форма.ФормаНастройки", П);
	
	Если Результат <> Неопределено Тогда
		
		Объект.ВыполнятьВТранзакции = Результат.ВыполнятьВТранзакции;
		Объект.ВыполнятьВРежимеЗагрузки = Результат.ВыполнятьВРежимеЗагрузки;
		Объект.НеПрерыватьВыполнениеПриОшибке = Результат.НеПрерыватьВыполнениеПриОшибке;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоТаблицИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанныеЭлемента = Элементы.ДеревоТаблиц.ТекущиеДанные;
	
	Если ТекущиеДанныеЭлемента <> Неопределено Тогда
		
		РодительТекущего = ТекущиеДанныеЭлемента.ПолучитьРодителя();
			
		Если РодительТекущего <> Неопределено Тогда
			
			Если ТекущиеДанныеЭлемента.Использовать = Истина Тогда
				
				РодительТекущего.Использовать = Истина;
				
			Иначе
				
				ОсталисьЗадействованные = Ложь;
				
				ЭлементыРодителя = РодительТекущего.ПолучитьЭлементы();
				
				Для Каждого ЭлементРодителя ИЗ ЭлементыРодителя Цикл
					
					Если ЭлементРодителя.Использовать = Истина Тогда
						
						ОсталисьЗадействованные  = Истина;
						
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
				РодительТекущего.Использовать = ОсталисьЗадействованные;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЭлементыТекущего = ТекущиеДанныеЭлемента.ПолучитьЭлементы();
		
		Если ЭлементыТекущего <> Неопределено Тогда
			
			Для Каждого ЭлементТекущего Из ЭлементыТекущего Цикл
				
				ЭлементТекущего.Использовать = ТекущиеДанныеЭлемента.Использовать;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаРезультатОтбор.Очистить();
	ПодготовитьСписокВыбранныхОбъектов();
		
КонецПроцедуры

&НаСервере
Процедура ПодготовитьСписокВыбранныхОбъектов()
	
	РезультатВыбора = Новый СписокЗначений;
	ПодготовитьСписокВыбранных(РезультатВыбора);

	КэшОтбора = Объект.Компоновщик.Настройки.Отбор.Элементы;
	
	Если РезультатВыбора <> Неопределено Тогда
			
			Если ОбрабатыватьТЧ = Ложь Тогда 
				
				ТаблицаМетаданных.Очистить();
				ТаблицаВидыОбъектов.Очистить();
				
				Для Каждого ЭлементРезультат Из РезультатВыбора Цикл
					
					НоваяСтрока = ТаблицаВидыОбъектов.Добавить();
					НоваяСтрока.ИмяТаблицы = ЭлементРезультат.Значение;
					НоваяСтрока.ПредставлениеТаблицы = ЭлементРезультат.Представление;
					
				КонецЦикла;
				
				ТаблицаРезультатОтбор.Очистить();
				
				ИнициализироватьСКД();
				
				Если Не  ВыберитеДействие = "ИзменитьРеквизиты" Тогда
					
					ВыберитеДействие = "";
					
				КонецЕсли;
				
			Иначе
				
				ТаблицаМетаданных.Очистить();
				ТаблицаВидыОбъектов.Очистить();
				ТаблицаРезультатОтбор.Очистить();
				
				Для Каждого ЭлементРезультат Из РезультатВыбора Цикл
					
					НоваяСтрока = ТаблицаВидыОбъектов.Добавить();
					НоваяСтрока.ИмяТаблицы = ЭлементРезультат.Значение.ИмяОбъекта;
					НоваяСтрока.ИмяТЧ = ЭлементРезультат.Значение.ИмяТЧ;
					НоваяСтрока.ПредставлениеТаблицы = ЭлементРезультат.Представление;
					
				КонецЦикла;
				
				ИнициализироватьСКД();
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИнициализироватьДействия();
		
	КэшВидимыхКолонок.Очистить();	
	
	ВосстановаитьОтборИзКэша(Объект.Компоновщик.Настройки.Отбор.Элементы, КэшОтбора, Объект.Компоновщик.Настройки.Отбор.ДоступныеПоляОтбора);
	
		
	ОчиститьУО();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьУО();
	
	Если ЭтаФорма.УсловноеОформление.Элементы.Количество() > 0 Тогда
		
		ЭтаФорма.УсловноеОформление.Элементы.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ВосстановаитьОтборИзКэша(ЭлементыОтбора, КэшОтбора, ОтборДоступныеПоляОтбора)
	
	Если КэшОтбора.Количество() > 0 Тогда
		
		Для Каждого ЭлементОтбора Из КэшОтбора Цикл
			
			Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				
				НовоеПоле = ЭлементыОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ЗаполнитьЗначенияСвойств(НовоеПоле,ЭлементОтбора);
				ВосстановаитьОтборИзКэша(НовоеПоле.Элементы, ЭлементОтбора.Элементы, ОтборДоступныеПоляОтбора)
				
			Иначе
				
			Если Объект.Компоновщик.Настройки.Отбор.ДоступныеПоляОтбора.НайтиПоле(ЭлементОтбора.ЛевоеЗначение) <> Неопределено Тогда
				
				 НовоеПоле  = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				 ЗаполнитьЗначенияСвойств(НовоеПоле, ЭлементОтбора);
				
			КонецЕсли;	
			
			КонецЕсли;
		
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПодготовитьСписокВыбранных(СписокВыбора)
	
	ДеревоМетаданных = РеквизитФормыВЗначение("ДеревоТаблиц");
	
	Если ОбрабатыватьТЧ = Ложь Тогда
		
		Для Каждого ЭлементМетаданных Из ДеревоМетаданных.Строки Цикл
			
			Если ЭлементМетаданных.Использовать Тогда
				
				СписокВыбора.Добавить(ЭлементМетаданных.ИмяТаблицы,ЭлементМетаданных.ПредставлениеТаблицы);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе 
		
		Для Каждого ЭлементМетаданных Из ДеревоМетаданных.Строки Цикл
			
			Если ЭлементМетаданных.Использовать Тогда
						
				Для Каждого ТЧОбъекта Из ЭлементМетаданных.Строки Цикл
					
					Если ТЧОбъекта.Использовать Тогда
						
						Структура = Новый Структура;
						Структура.Вставить("ИмяОбъекта", ЭлементМетаданных.ИмяТаблицы);
						Структура.Вставить("ИмяТЧ", ТЧОбъекта.ИмяТаблицы);
						
						СписокВыбора.Добавить(Структура , ЭлементМетаданных.ПредставлениеТаблицы + " [ ТЧ: " + ТЧОбъекта.ПредставлениеТаблицы + " ]");
						
					КонецЕсли;
					
				КонецЦикла;
				
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиПорядокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаУстановитьВсеОбъекты(Команда)
	
	УставновитьВсеОбъекты(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УставновитьВсеОбъекты(НовоеЗначение)
	
	НаборСтрокДерева  = ДеревоТаблиц.ПолучитьЭлементы();
	
	Для Каждого СтрокаДерева Из НаборСтрокДерева Цикл
		
		СтрокаДерева.Использовать = НовоеЗначение;
		
		Для Каждого СтрокаПодчиненная Из СтрокаДерева.ПолучитьЭлементы() Цикл
			
			СтрокаПодчиненная.Использовать = НовоеЗначение;
			
		КонецЦикла;		
		
	КонецЦикла;
	
	ПодготовитьСписокВыбранныхОбъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСнятьВсеОбъекты(Команда)
	
	  УставновитьВсеОбъекты(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияХарактеристикиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОтборПоТипуДляХарактеристик =  Истина;

	ТипыВыбранные = ТипЗначенияХарактеристики.Типы();
	ИнициализироватьДопОтборХарактеристик();
	П = Новый Структура;
	П.Вставить("МассивТипов", ТипыВыбранные);
	П.Вставить("ДоступныеТипы", ТипЗначенияХарактеристики);
		
	Результат = 	ОткрытьФормуМодально("Обработка.ГрупповаяОбработкаСправочниковИДокументов.Форма.ФормаВыбораТиповХарактеристики", П);
	
	Если Результат <> Неопределено Тогда
		
		МассивТипов = Новый Массив;
		
		Для Каждого ВыбранныйТип Из Результат Цикл
			
			Если ВыбранныйТип.Пометка Тогда
				
				МассивТипов.Добавить(ВыбранныйТип.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ТипЗначенияХарактеристики = Новый ОписаниеТипов(МассивТипов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	ЗаполнитьНастройкиПриСохраненииНаСервере(Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПриСохраненииНаСервере(Настройки)
	
	Если Настройки = Неопределено Тогда
		
		Настройки = Новый Соответствие;
		
	КонецЕсли;
	
	Настройки.Вставить("ОбрабатыватьТЧ", ОбрабатыватьТЧ);
	Настройки.Вставить("ТаблицаВидыОбъектов", ТаблицаВидыОбъектов.Выгрузить());
	Настройки.Вставить("РезультатВыбора", РезультатВыбора);
	Настройки.Вставить("ТипОбъекта", ТипОбъекта);
	
	Настройки.Вставить("ВыполнятьВТранзакции", Объект.ВыполнятьВТранзакции);
	Настройки.Вставить("ВыполнятьВРежимеЗагрузки", Объект.ВыполнятьВРежимеЗагрузки);
	
	Настройки.Вставить("ОтборПоТипуДляХарактеристик", ОтборПоТипуДляХарактеристик);
	Настройки.Вставить("РежимОтбораХарактеристик", РежимОтбораХарактеристик);
	Настройки.Вставить("ТипЗначенияХарактеристики", ТипЗначенияХарактеристики);	
	Настройки.Вставить("КэшВидимыхКолонок", КэшВидимыхКолонок);	
	Настройки.Вставить("НеПрерыватьВыполнениеПриОшибке", Объект.НеПрерыватьВыполнениеПриОшибке);	
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	ЗаписьXML.ЗаписатьНачалоЭлемента("Отборы");	
	
	СериализоватьОтбор(Объект.Компоновщик.Настройки.Отбор.Элементы, ЗаписьXML);
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ХранилищеНастроек = ЗаписьXML.Закрыть();
	
	Настройки.Вставить("Отбор", ХранилищеНастроек);

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ВосстановитьНастройки(Настройки);
	
КонецПроцедуры

&НаСервере
Процедура  ВосстановитьНастройки(Настройки)
	
	  Если ТипЗнч(Настройки) = Тип("Соответствие") Тогда
		
		ОбрабатыватьТЧ = Настройки["ОбрабатыватьТЧ"];
		ТипОбъекта = Настройки["ТипОбъекта"];
		
		Если Настройки["ТаблицаВидыОбъектов"] <> Неопределено Тогда
			
			ТаблицаВидыОбъектов.Загрузить(Настройки["ТаблицаВидыОбъектов"]);
			
		КонецЕсли;
		
		Для Каждого ЭлементВыборка Из Настройки["РезультатВыбора"] Цикл
			
			РезультатВыбора.Добавить(ЭлементВыборка.Значение, ЭлементВыборка.Представление);
			
		КонецЦикла;
		
		
		Если Настройки.Получить("ВыполнятьВТранзакции") <> Неопределено Тогда
			
			Объект.ВыполнятьВТранзакции = Настройки.Получить("ВыполнятьВТранзакции");
			
		КонецЕсли;
		
		Если Настройки.Получить("ВыполнятьВРежимеЗагрузки") <> Неопределено Тогда
			
			Объект.ВыполнятьВРежимеЗагрузки = Настройки.Получить("ВыполнятьВРежимеЗагрузки");
			
		КонецЕсли;
		
		Если  Настройки.Получить("ОтборПоТипуДляХарактеристик") <> Неопределено Тогда
			
			ОтборПоТипуДляХарактеристик = Настройки.Получить("ОтборПоТипуДляХарактеристик");
			
		КонецЕсли;			
		
		Если  Настройки.Получить("РежимОтбораХарактеристик") <> Неопределено Тогда
			
			РежимОтбораХарактеристик = Настройки.Получить("РежимОтбораХарактеристик");
			
		КонецЕсли;	
		
		Если  Настройки.Получить("ТипЗначенияХарактеристики") <> Неопределено Тогда
			
			ТипЗначенияХарактеристики = Настройки.Получить("ТипЗначенияХарактеристики");
			
		КонецЕсли;	
		
		Если  Настройки.Получить("КэшВидимыхКолонок") <> Неопределено Тогда
			
			Для Каждого КэшированнаяКолонка Из  Настройки.Получить("КэшВидимыхКолонок") Цикл
				
				КэшВидимыхКолонок.Добавить(КэшированнаяКолонка.Значение);
				
			КонецЦикла;			
			
		КонецЕсли;	

		Если Настройки.Получить("НеПрерыватьВыполнениеПриОшибке") <> Неопределено Тогда
			
			Объект.НеПрерыватьВыполнениеПриОшибке =Настройки.Получить("НеПрерыватьВыполнениеПриОшибке"); 
			
		КонецЕсли;
		
	Иначе
		
		 ТипОбъекта = "Справочники";
		
	КонецЕсли;

	Попытка
			
			ПерезаполнитьДеревоТаблиц();
			ИнициализироватьСКД();
			ДесериализоватьОтбор(Настройки["Отбор"], Объект.Компоновщик.Настройки.Отбор);
			
	Исключение
			
			//Не удалось восстановить настройки (например, конфигурация была изменена)
			
	КонецПопытки;

	Если СтрДлина(РежимОтбораХарактеристик) = 0 Тогда
		
		РежимОтбораХарактеристик = "По выбранным значениям";
		
	КонецЕсли;
	
	Если ВыберитеДействие <> "ИзменитьРеквизиты" Тогда
		
		Элементы.ГруппаРедактироватьРеквизиты.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаРедактироватьРеквизиты.Видимость = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВсеВозможныеПоляОтбора(Команда)
	
	ДобавитьВсеВозможныеПоляОтбораСервер();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВсеВозможныеПоляОтбораСервер()
	
	Для Каждого ДоступноеПолеОтбора Из Объект.Компоновщик.Настройки.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		
		   НовоеПолеОтбора = Объект.Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		   НовоеПолеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ДоступноеПолеОтбора.Поле);
		   
		   Если ДоступноеПолеОтбора.ДоступныеВидыСравнения.Количество() > 0 Тогда
			   
			   НовоеПолеОтбора.ВидСравнения =  ДоступноеПолеОтбора.ДоступныеВидыСравнения.Получить(0).Значение;
			   
		   КонецЕсли;
		   
	    НовоеПолеОтбора.Использование = Ложь;
		 
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	     
	Если ВыберитеДействие = "ИзменитьРеквизиты" Тогда
		
		ПодготовитьДействиеИзменитьРеквизиты();
		
	КонецЕсли;
	
	Элементы.ДеревоТаблицГруппаСвернутьРазвернуть.Видимость = ОбрабатыватьТЧ;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	УстановитьОграничениеТипов(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитыПередНачаломИзменения(Элемент, Отказ)
	
	УстановитьОграничениеТипов(Элемент);
	
КонецПроцедуры

 &НаКлиенте
Процедура УстановитьОграничениеТипов(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаРеквизиты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Элемент.ПодчиненныеЭлементы.ТаблицаРеквизитыНовоеЗначение.ОграничениеТипа = ТекущиеДанные.ДоступныеТипыЗначений;
	
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ТаблицаРеквизиты.ТекущиеДанные;
	  
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.Изменять = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитыНовоеЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные =  Элементы.ТаблицаРеквизиты.ТекущиеДанные;
	УстановитьВидимостьКолонокПоДопРеквизитам(ТекущиеДанные);
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКолонокПоДопРеквизитам(ТекущаяСтрока)
	
	ЭлементКолонка = Элементы.ТаблицаРезультатОтбор.ПодчиненныеЭлементы.ТаблицаРезультатОтборПрограммно.ПодчиненныеЭлементы.Найти(ТекущаяСтрока.ИмяРеквизита); 
	
	Если ЭлементКолонка <> Неопределено Тогда
		
		   ЭлементКолонка.Видимость = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоТаблицСвернуть(Команда)
	
	Если Команда.Имя = "ДеревоТаблицРазвернуть" Тогда
		
		ВыполняемоеДействие = "Развернуть";
		
	Иначе
		
		ВыполняемоеДействие = "Свернуть";
		
	КонецЕсли;
	
	СвернутьРазвернутьСписокТаблиц(ВыполняемоеДействие);
	
КонецПроцедуры

&НаКлиенте	
Процедура СвернутьРазвернутьСписокТаблиц(ВыполняемоеДействие)
	
	ЭлементыДерева = ДеревоТаблиц.ПолучитьЭлементы();
	
	Для Каждого СтрокаСпискаТаблиц Из ЭлементыДерева  Цикл
		
		Если ВыполняемоеДействие = "Свернуть" Тогда
			
			Элементы.ДеревоТаблиц.Свернуть(СтрокаСпискаТаблиц.ПолучитьИдентификатор());
		
		Иначе 
			
			Элементы.ДеревоТаблиц.Развернуть(СтрокаСпискаТаблиц.ПолучитьИдентификатор());
				
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВсеВозможныеПоляСортировки(Команда)
	
	   Для Каждого ДоступноеПолеСортировки Из Объект.Компоновщик.Настройки.Порядок.ДоступныеПоляПорядка.Элементы Цикл
		
		   НовоеПолеПорядка = Объект.Компоновщик.Настройки.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		   
		   НовоеПолеПорядка.Поле = Новый ПолеКомпоновкиДанных(ДоступноеПолеСортировки.Поле);
		   НовоеПолеПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		   
	       НовоеПолеПорядка.Использование = Ложь;
		 
	КонецЦикла;

	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуОбработки(Команда)	
	
	ПерейтиПоНавигационнойСсылке("http://infostart.ru/public/189338/");	
КонецПроцедуры
