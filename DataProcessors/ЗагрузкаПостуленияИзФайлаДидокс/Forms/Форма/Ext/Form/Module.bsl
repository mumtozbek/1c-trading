
&НаСервере
Перем Excel;

&НаКлиенте
Процедура СтрокиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СтрокиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры


&НаСервере
Функция НайтиПоступления(Номер, Дата, Контрагент)
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Ссылка КАК Документ
	|ИЗ
	|	Документ.ПоступлениеТМЦ КАК Документы
	|ГДЕ
	|	ИСТИНА
	|	И Документы.НомерВходящегоДокумента = &Номер
	|	И Документы.ДатаВходящегоДокумента = &Дата
	|	И Документы.Контрагент = &Контрагент
	|");
	
	Запрос.УстановитьПараметр("Номер", Номер);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Документ;
	КонецЕсли;
КонецФункции

&НаСервере
Функция НайтиТовара(Реквизиты, Дата)
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Ссылка КАК Товар,
	|	Цены.Цена КАК ЦенаБазовая
	|ИЗ
	|	Справочник.Товары КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныТоваров.СрезПоследних(&Дата, Тип = ""ЦенаБазовая"") КАК Цены ПО Цены.Товар.Ссылка = Товары.Ссылка
	|ГДЕ
	|	ИСТИНА
	|	И (Товары.КодИКПУ = &ВнешнийКод ИЛИ Товары.ШтрихКод = &ШтрихКод)
	|	И Цены.Цена = &ЦенаБазовая
	|");
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВнешнийКод", Реквизиты.ВнешнийКод);
	Запрос.УстановитьПараметр("ШтрихКод", Реквизиты.ШтрихКод);
	Запрос.УстановитьПараметр("ЦенаБазовая", Реквизиты.ЦенаБазовая);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Товар;
	Иначе
		НовыйОбъект = Справочники.Товары.СоздатьЭлемент();
		НовыйОбъект.Наименование = Реквизиты.Наименование;
		НовыйОбъект.ВнешнийКод = Реквизиты.ВнешнийКод;
		НовыйОбъект.ШтрихКод = Реквизиты.ШтрихКод;
		НовыйОбъект.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.Штуки;
		НовыйОбъект.Поставщик = Реквизиты.Поставщик;
		НовыйОбъект.Объем = Реквизиты.Объем;
		НовыйОбъект.СчетУчета = ПланыСчетов.Основной.НайтиПоКоду("2910");
		НовыйОбъект.ВидТовара = НайтиВидТовара(Реквизиты.Вид);
		НовыйОбъект.Записать();
		
		Возврат НовыйОбъект.Ссылка;
	КонецЕсли;
КонецФункции

&НаСервере
Функция НайтиВидТовара(Наименование)
	Если ПустаяСтрока(Наименование) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Справочники.ВидыТовара.НайтиПоНаименованию(Наименование);
	
	Если ЗначениеЗаполнено(Выборка) Тогда
		Возврат Выборка.Ссылка;
	Иначе
		НовыйОбъект = Справочники.ВидыТовара.СоздатьЭлемент();
		НовыйОбъект.Наименование = Наименование;
		НовыйОбъект.Записать();
		
		Возврат НовыйОбъект.Ссылка;
	КонецЕсли;
КонецФункции


&НаКлиенте
Процедура ФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "Excel (*.xls,*.xlsx)|*.xls;*.xlsx";

	Если Диалог.Выбрать() Тогда
		Файл = Диалог.ПолноеИмяФайла;
		ДвоичныеДанные = Новый ДвоичныеДанные(Файл);
		АдресВременногоХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ЭтаФорма.УникальныйИдентификатор);
		ОткрытьФайл(АдресВременногоХранилища);
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ОткрытьФайл(АдресВременногоХранилища)
	ИмяФайла = ПолучитьИмяВременногоФайла(".xls");
	
	ВременныйФайл = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	ВременныйФайл.Записать(ИмяФайла);
	
	// Закрываем файл, если он открыт
	ЗакрытьФайл();
	
	// Подключаемся к эксель
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Excel.WorkBooks.Open(ИмяФайла);
	Исключение
		Сообщить("Ошибка при открытии файла с помощью Excel!");
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;

	Попытка
		// Открываем необходимый лист
		Excel.Sheets(1).Select(); // лист 1, по умолчанию
	Исключение
		// Закрываем Excel
		ЗакрытьФайл();
	КонецПопытки;
	
	Если Лев(Excel.Version,Найти(Excel.Version,".") - 1) = "8" тогда
		КоличествоСтрок = Excel.Cells.CurrentRegion.Rows.Count;
	Иначе
		КоличествоСтрок = Excel.Cells(1, 1).SpecialCells(11).Row;
	Конецесли;
	
	// Регулярные выражения
	RegExp = Новый COMОбъект("VBScript.RegExp");
	RegExp.IgnoreCase = Ложь;
	RegExp.Global = Истина;
	RegExp.MultiLine = Ложь;
	
	// Шапка
	Организация = Справочники.Организации.НайтиПоРеквизиту("ИНН", СокрЛП(Excel.Cells(8, 11).Text));
	
	RegExp.Pattern = "№ ([\S]{0,}) от (\w+\.\w+\.\w+)";
	Matches = RegExp.Execute(СокрЛП(Excel.Cells(3, 1).Text));
	Если Matches.Count() > 0 И Matches.Item(0).SubMatches.Count() = 2 Тогда
		НомерВходящегоДокумента = СокрЛП(Matches.Item(0).SubMatches.Item(0));
		ДатаВходящегоДокумента = Дата(СокрЛП(Matches.Item(0).SubMatches.Item(1)) + " 00:00:00");
	КонецЕсли;
	
	Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", СокрЛП(Excel.Cells(8, 3).Text));
	
	RegExp.Pattern = "к договору № ([\S]{0,}) от (\w+\.\w+\.\w+)";
	Matches = RegExp.Execute(СокрЛП(Excel.Cells(4, 1).Text));
	Если Matches.Count() > 0 И Matches.Item(0).SubMatches.Count() = 2 Тогда
		Договор = Справочники.Договоры.НайтиДоговора(Контрагент, СокрЛП(Matches.Item(0).SubMatches.Item(0)), Дата(СокрЛП(Matches.Item(0).SubMatches.Item(1)) + " 00:00:00"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Договор) = Ложь Тогда
		Договор = Контрагент.ОсновнойДоговор;
	КонецЕсли;
	
	// Строки
	Строки.Очистить();
	Для НС = 17 по КоличествоСтрок - 1 Цикл
		Если СокрЛП(Excel.Cells(НС, 1).Text) = "Итого" Тогда
			Прервать;
		Иначе
			НоваяСтрока = Строки.Добавить();
			НоваяСтрока.НомерСтроки = СокрЛП(Excel.Cells(НС, 1).Text);
			НоваяСтрока.Наименование = Прав(СокрЛП(Excel.Cells(НС, 4).Text), СтрДлина(СокрЛП(Excel.Cells(НС, 4).Text)) - 20);
			НоваяСтрока.ВнешнийКод = Лев(СокрЛП(Excel.Cells(НС, 4).Text), 17);
			НоваяСтрока.ШтрихКод = Формат(Число(Excel.Cells(НС, 5).Value), "ЧГ=0");
			НоваяСтрока.Количество = Excel.Cells(НС, 7).Value;
			НоваяСтрока.ЦенаБазовая = Excel.Cells(НС, 11).Value / Excel.Cells(НС, 7).Value;
			НоваяСтрока.СуммаБазовая = Excel.Cells(НС, 11).Value;
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.НайтиПоРеквизиту("Ставка", Число(Excel.Cells(НС, 12).Value*100));
			НоваяСтрока.СуммаНДС = Excel.Cells(НС, 13).Value;
			НоваяСтрока.ЦенаПродажная = Excel.Cells(НС, 14).Value / Excel.Cells(НС, 7).Value;
			НоваяСтрока.СуммаПродажная = Excel.Cells(НС, 14).Value;
			
			RegExp.Pattern = " ([0-9,]+) ";
			Matches = RegExp.Execute(СокрЛП(Excel.Cells(НС, 6).Text));
			Если Matches.Count() > 0 И Matches.Item(0).SubMatches.Count() = 1 Тогда
				НоваяСтрока.Объем = СокрЛП(Matches.Item(0).SubMatches.Item(0));
			КонецЕсли;
			
			RegExp.Pattern = "^([\S]+) ";
			Matches = RegExp.Execute(НоваяСтрока.Наименование);
			Если Matches.Count() > 0 И Matches.Item(0).SubMatches.Count() = 1 Тогда
				НоваяСтрока.Вид = СокрЛП(Matches.Item(0).SubMatches.Item(0));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Подвал
	RegExp.Pattern = "№ ([\S]{0,}) от (\w+\.\w+\.\w+)";
	Matches = RegExp.Execute(СокрЛП(Excel.Cells(НС + 8, 9).Text));
	Если Matches.Count() > 0 И Matches.Item(0).SubMatches.Count() = 2 Тогда
		НомерДоверенности = СокрЛП(Matches.Item(0).SubMatches.Item(0));
		ДатаДоверенности = Дата(СокрЛП(Matches.Item(0).SubMatches.Item(1)) + " 00:00:00");
	КонецЕсли;
	
	Экспедитор = СокрЛП(Excel.Cells(НС + 10, 8).Text);
	
	Файл = ИмяФайла;
	
	// Закрываем файл, если он открыт
	ЗакрытьФайл();
КонецПроцедуры

&НаСервере
Процедура ЗакрытьФайл()
	// Закрываем файл, если он уже открыт
	Если ТипЗнч(Excel) = Тип("COMОбъект") Тогда
		Excel.DisplayAlerts = 0; 
		Excel.Quit();
		Excel.DisplayAlerts = 1;
		Excel = Неопределено;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура Загрузить(Команда)
	ЗагрузитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере()
	Если ЗначениеЗаполнено(Дата) = Ложь Тогда
		Сообщить("Дата не указана!");
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Дата;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) = Ложь Тогда
		Сообщить("Организация не указана!");
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Организация;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) = Ложь Тогда
		Сообщить("Склад не указан!");
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Склад;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) = Ложь Тогда
		Сообщить("Контрагент не указан!");
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.Контрагент;
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = НайтиПоступления(НомерВходящегоДокумента, ДатаВходящегоДокумента, Контрагент);
	УчетнаяПолитика = УчетныеПолитики.ПолучитьНаДату(Дата, Организация);
	
	Если ЗначениеЗаполнено(ДокументСсылка) = Ложь Тогда
		ДокументОбъект = Документы.ПоступлениеТМЦ.СоздатьДокумент();
	Иначе
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		ДокументОбъект.Товары.Очистить();
	КонецЕсли;
	
	ДокументОбъект.Дата = Дата;
	ДокументОбъект.Организация = Организация;
	ДокументОбъект.Склад = Склад;
	ДокументОбъект.Контрагент = Контрагент;
	ДокументОбъект.Договор = Договор;
	ДокументОбъект.НомерВходящегоДокумента = НомерВходящегоДокумента;
	ДокументОбъект.ДатаВходящегоДокумента = ДатаВходящегоДокумента;
	ДокументОбъект.НомерДоверенности = НомерДоверенности;
	ДокументОбъект.ДатаДоверенности = ДатаДоверенности;
	ДокументОбъект.Экспедитор = Экспедитор;
	ДокументОбъект.СтавкаНДС = УчетнаяПолитика.СтавкаНДС;
	ДокументОбъект.УчетПоСальдо = Истина;
	
	Для Каждого Строка Из Строки Цикл
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		НоваяСтрока.Товар = НайтиТовара(Новый Структура("Наименование,ВнешнийКод,ШтрихКод,Вид,Объем,ЦенаБазовая,Поставщик", Строка.Наименование, Строка.ВнешнийКод, Строка.ШтрихКод, Строка.Вид, Строка.Объем, Строка.ЦенаБазовая, Контрагент), Дата);
		НоваяСтрока.Количество = Строка.Количество;
		НоваяСтрока.ЦенаБазовая = Строка.ЦенаБазовая;
		НоваяСтрока.СуммаБазовая = Строка.СуммаБазовая;
		НоваяСтрока.СтавкаНДС = Строка.СтавкаНДС;
		НоваяСтрока.СуммаНДС = Строка.СуммаНДС;
		НоваяСтрока.ЦенаПродажная = Строка.ЦенаПродажная;
		НоваяСтрока.СуммаПродажная = Строка.СуммаПродажная;
		НоваяСтрока.ЦенаОтпускная = РаботаСТоварами.Цена(НоваяСтрока.Товар, "ЦенаПродажная", ДокументОбъект.Дата, НоваяСтрока.ЦенаОтпускная);
		НоваяСтрока.СуммаОтпускная = НоваяСтрока.ЦенаОтпускная * НоваяСтрока.Количество;
		НоваяСтрока.Объем = Строка.Объем * Строка.Количество;
		НоваяСтрока.Вес = НоваяСтрока.Товар.Вес * НоваяСтрока.Количество;
	КонецЦикла;
	
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Сообщить("Документ загружен " + СокрЛП(ДокументОбъект.Ссылка));
КонецПроцедуры
