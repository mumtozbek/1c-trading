
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Основной");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьГруппа = Макет.ПолучитьОбласть("Группа");
	ОбластьТовар = Макет.ПолучитьОбласть("Товар");
	ОбластьОбщийИтог = Макет.ПолучитьОбласть("ОбщиеИтоги");
	
	ОбластьЗаголовок.Параметры.Дата = Дата;
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РегОстатки.Склад КАК Склад,
		|	РегОстатки.Товар КАК Товар,
		|	(РегОстатки.КоличествоКонечныйОстаток) КАК Количество,
		|	-(РегБрони.КоличествоКонечныйОстаток) КАК Забронировано
		|ИЗ
		|	РегистрНакопления.Остатки.ОстаткиИОбороты(&Дата, &Дата, Запись, ДвиженияИГраницыПериода, Порожняя = &Порожняя И Бронь = Ложь) КАК РегОстатки
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Остатки.ОстаткиИОбороты(&Дата, &Дата, Запись, ДвиженияИГраницыПериода, Порожняя = &Порожняя И Бронь = Истина) КАК РегБрони ПО РегОстатки.Товар = РегБрони.Товар И РегОстатки.Склад = РегБрони.Склад
		|ГДЕ
		|	РегОстатки.Товар.ВидТовара.ТипТовара = ЗНАЧЕНИЕ(Перечисление.ТипыТовара.Товар)"
	);
	
	Если НЕ Склад.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И РегОстатки.Склад = &Склад";
	КонецЕсли;
	
	Если НЕ Товар.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И (РегОстатки.Товар.Ссылка = &Товар ИЛИ РегОстатки.Товар.Ссылка В ИЕРАРХИИ (&Товар))";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	//|СГРУППИРОВАТЬ ПО
	//|	РегОстатки.Склад,
	//|	РегОстатки.Товар
	|УПОРЯДОЧИТЬ ПО
	|	РегОстатки.Товар.Наименование,
	|	РегОстатки.Товар.Код
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Забронировано)
	|ПО
	|	ОБЩИЕ,
	|	РегОстатки.Товар" + ?(ПоГруппам, " ИЕРАРХИЯ", "") + " 
	|";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Товар", Товар);
	Запрос.УстановитьПараметр("Порожняя", Порожняя);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТабДок.НачатьАвтогруппировкуСтрок();

	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбщийИтог.Следующий() Цикл
		ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		ОбластьОбщийИтог.Параметры.Свободно = ?(ТипЗнч(ВыборкаОбщийИтог.Количество) = Тип("Число"), ВыборкаОбщийИтог.Количество, 0) - ?(ТипЗнч(ВыборкаОбщийИтог.Забронировано) = Тип("Число"), ВыборкаОбщийИтог.Забронировано, 0);
		ТабДок.Вывести(ОбластьОбщийИтог, ВыборкаОбщийИтог.Уровень());

		ВыборкаТовар = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаТовар.Следующий() Цикл
			Если ВыборкаТовар.Товар.ЭтоГруппа Тогда
				ОбластьГруппа.Параметры.Заполнить(ВыборкаТовар);
				ОбластьГруппа.Параметры.Свободно = ?(ТипЗнч(ВыборкаТовар.Количество) = Тип("Число"), ВыборкаТовар.Количество, 0) - ?(ТипЗнч(ВыборкаТовар.Забронировано) = Тип("Число"), ВыборкаТовар.Забронировано, 0);
				ТабДок.Вывести(ОбластьГруппа, ВыборкаТовар.Уровень());
			Иначе
				ОбластьТовар.Параметры.Заполнить(ВыборкаТовар);
				ОбластьТовар.Параметры.Свободно = ?(ТипЗнч(ВыборкаТовар.Количество) = Тип("Число"), ВыборкаТовар.Количество, 0) - ?(ТипЗнч(ВыборкаТовар.Забронировано) = Тип("Число"), ВыборкаТовар.Забронировано, 0);
				ТабДок.Вывести(ОбластьТовар, ВыборкаТовар.Уровень());
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
КонецПроцедуры