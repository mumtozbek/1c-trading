
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Основной");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьОбщийИтог = Макет.ПолучитьОбласть("ОбщиеИтоги");
	ОбластьСотрудник = Макет.ПолучитьОбласть("Сотрудник");
	ОбластьРегистратор = Макет.ПолучитьОбласть("Регистратор");

	ОбластьЗаголовок.Параметры.Период = ПредставлениеПериода(НачалоДня(Период.ДатаНачала), КонецДня(Период.ДатаОкончания));
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОтгрузкиОбороты.Сотрудник КАК Сотрудник,
	|	ОтгрузкиОбороты.Регистратор КАК Регистратор,
	|	ОтгрузкиОбороты.Примечание КАК Примечание,
	|	СУММА(ОтгрузкиОбороты.СуммаОборот) КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.Отгрузки.Обороты(&Дата1, &Дата2, Регистратор) КАК ОтгрузкиОбороты
	|ГДЕ
	|	ИСТИНА");
	
	Если НЕ Сотрудник.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Сотрудник = &Сотрудник";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|СГРУППИРОВАТЬ ПО
	|	Сотрудник,
	|	Регистратор,
	|	Примечание
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Регистратор.Дата
	|ИТОГИ
	|	СУММА(СуммаОборот)
	|ПО
	|	ОБЩИЕ,
	|	Сотрудник,
	|	Регистратор,
	|	Примечание";

	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);

	РезультатЗапроса = Запрос.Выполнить();
	
	ТабДок.НачатьАвтогруппировкуСтрок();

	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    Пока ВыборкаОбщийИтог.Следующий() Цикл
		ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(ОбластьОбщийИтог, ВыборкаОбщийИтог.Уровень());

		ВыборкаСотрудник = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСотрудник.Следующий() Цикл
			ОбластьСотрудник.Параметры.Заполнить(ВыборкаСотрудник);
			ТабДок.Вывести(ОбластьСотрудник, ВыборкаСотрудник.Уровень());

			Если Подробно Тогда
				ВыборкаРегистратор = ВыборкаСотрудник.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаРегистратор.Следующий() Цикл
					Если ВыборкаРегистратор.Регистратор <> Неопределено И ВыборкаРегистратор.Регистратор.Дата >= НачалоДня(Период.ДатаНачала) И ВыборкаРегистратор.Регистратор.Дата <= КонецДня(Период.ДатаОкончания) Тогда
						ОбластьРегистратор.Параметры.Заполнить(ВыборкаРегистратор);
						
						ВыборкаКомментарий = ВыборкаРегистратор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Если ВыборкаКомментарий.Следующий() Тогда
							Если ЗначениеЗаполнено(ВыборкаКомментарий.Примечание) Тогда
								ОбластьРегистратор.Параметры.Заполнить(ВыборкаКомментарий);
							КонецЕсли;
						КонецЕсли;
						
						ТабДок.Вывести(ОбластьРегистратор, ВыборкаРегистратор.Уровень());
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ТабДок.ЗакончитьАвтогруппировкуСтрок();

	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры