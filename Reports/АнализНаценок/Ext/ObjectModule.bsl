
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Основной");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьОбщийИтог = Макет.ПолучитьОбласть("ОбщиеИтоги");
	ОбластьВидРеализации = Макет.ПолучитьОбласть("ВидРеализации");
	ОбластьРегистратор = Макет.ПолучитьОбласть("Регистратор");
	
    ОбластьЗаголовок.Параметры.Период = ПредставлениеПериода(НачалоДня(Период.ДатаНачала), КонецДня(Период.ДатаОкончания));
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПродажиОбороты.ВидРеализации КАК ВидРеализации,
	|	ПродажиОбороты.Регистратор КАК Регистратор,
	|	СУММА(ПродажиОбороты.СуммаБазоваяОборот) КАК СуммаБазовая,
	|	СУММА(ПродажиОбороты.СуммаСНаценкойОборот) КАК СуммаСНаценкой
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&Дата1, &Дата2, Регистратор) КАК ПродажиОбороты
	|ГДЕ
	|	ИСТИНА");
	
	Если ЗначениеЗаполнено(ВидРеализации) Тогда
		Запрос.Текст = Запрос.Текст + " И ВидРеализации = &ВидРеализации";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|СГРУППИРОВАТЬ ПО
	|	ВидРеализации,
	|	Регистратор
	|УПОРЯДОЧИТЬ ПО
	|	ВидРеализации.Наименование
	|ИТОГИ
	|	СУММА(СуммаБазовая),
	|	СУММА(СуммаСНаценкой)
	|ПО
	|	ОБЩИЕ,
	|	ВидРеализации,
	|	Регистратор";

	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("ВидРеализации", ВидРеализации);

	РезультатЗапроса = Запрос.Выполнить();

	ТабДок.НачатьАвтогруппировкуСтрок();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    Пока ВыборкаОбщийИтог.Следующий() Цикл
		ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		ОбластьОбщийИтог.Параметры.Наценка = АрифметическиеФункции.ОпределитьНаценку(ВыборкаОбщийИтог.СуммаСНаценкой, ВыборкаОбщийИтог.СуммаБазовая);
		ТабДок.Вывести(ОбластьОбщийИтог, ВыборкаОбщийИтог.Уровень());

		ВыборкаВидРеализации = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаВидРеализации.Следующий() Цикл
			СтруктураРасшифровки = Новый Структура;
			СтруктураРасшифровки.Вставить("ВидРеализации", ВыборкаВидРеализации.ВидРеализации);
			
			ОбластьВидРеализации.Параметры.Заполнить(ВыборкаВидРеализации);
			ОбластьВидРеализации.Параметры.Расшифровка = СтруктураРасшифровки;
			ОбластьВидРеализации.Параметры.Наценка = АрифметическиеФункции.ОпределитьНаценку(ВыборкаВидРеализации.СуммаСНаценкой, ВыборкаВидРеализации.СуммаБазовая);
			ТабДок.Вывести(ОбластьВидРеализации, ВыборкаВидРеализации.Уровень());
			
			Если Подробно Тогда
				ВыборкаРегистратор = ВыборкаВидРеализации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаРегистратор.Следующий() Цикл
					ОбластьРегистратор.Параметры.Заполнить(ВыборкаРегистратор);
					ОбластьРегистратор.Параметры.Наценка = АрифметическиеФункции.ОпределитьНаценку(ВыборкаРегистратор.СуммаСНаценкой, ВыборкаРегистратор.СуммаБазовая);
					ТабДок.Вывести(ОбластьРегистратор, ВыборкаРегистратор.Уровень());
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ТабДок.ЗакончитьАвтогруппировкуСтрок();

	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры
