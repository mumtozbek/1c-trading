
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Основной");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьОбщийИтог = Макет.ПолучитьОбласть("ОбщиеИтоги");
	ОбластьКасса = Макет.ПолучитьОбласть("Касса");
	ОбластьСтатья = Макет.ПолучитьОбласть("Статья");
	ОбластьРегистратор = Макет.ПолучитьОбласть("Регистратор");
	ОбластьКомментарий = Макет.ПолучитьОбласть("Комментарий");
	
	ОбластьЗаголовок.Параметры.Период = ПредставлениеПериода(НачалоДня(Период.ДатаНачала), КонецДня(Период.ДатаОкончания));
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	РегКассаОстаткиИОбороты.Касса КАК Касса,
	|	РегКассаОстаткиИОбороты.Статья КАК Статья,
	|	РегКассаОстаткиИОбороты.Регистратор КАК Регистратор,
	|	РегКассаОстаткиИОбороты.Примечание КАК Примечание,
	|	СУММА(РегКассаОстаткиИОбороты.СуммаНачальныйОстаток) КАК СуммаНачальныйОстаток,
	|	СУММА(РегКассаОстаткиИОбороты.СуммаПриход) КАК СуммаПриход,
	|	СУММА(РегКассаОстаткиИОбороты.СуммаРасход) КАК СуммаРасход,
	|	СУММА(РегКассаОстаткиИОбороты.СуммаКонечныйОстаток) КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.Касса.ОстаткиИОбороты(&Дата1, &Дата2, Регистратор, ДвиженияИГраницыПериода) КАК РегКассаОстаткиИОбороты
	|ГДЕ
	|	ИСТИНА");
	
	Если НЕ Касса.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Касса = &Касса";
	КонецЕсли;
	
	Если НЕ Статья.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Статья = &Статья";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|СГРУППИРОВАТЬ ПО
	|	Касса,
	|	Статья,
	|	Регистратор,
	|	Примечание
	|УПОРЯДОЧИТЬ ПО
	|	Касса.Наименование,
	|	Статья.Наименование,
	|	Регистратор.Дата
	|ИТОГИ
	|	СУММА(СуммаНачальныйОстаток),
	|	СУММА(СуммаПриход),
	|	СУММА(СуммаРасход),
	|	СУММА(СуммаКонечныйОстаток)
	|ПО
	|	ОБЩИЕ,
	|	Касса,
	|	Статья,
	|	Регистратор,
	|	Примечание";

	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Касса", Касса);
	Запрос.УстановитьПараметр("Статья", Статья);

	РезультатЗапроса = Запрос.Выполнить();

	ТабДок.НачатьАвтогруппировкуСтрок();

	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбщийИтог.Следующий() Цикл
		ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(ОбластьОбщийИтог, ВыборкаОбщийИтог.Уровень());

		ВыборкаКасса = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКасса.Следующий() Цикл
			СтруктураРасшифровки = Новый Структура;
			СтруктураРасшифровки.Вставить("Касса", ВыборкаКасса.Касса);
			
			ОбластьКасса.Параметры.Заполнить(ВыборкаКасса);
			ОбластьКасса.Параметры.Расшифровка = СтруктураРасшифровки;
			ТабДок.Вывести(ОбластьКасса, ВыборкаКасса.Уровень());
			
			ВыборкаСтатья = ВыборкаКасса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСтатья.Следующий() Цикл
				СтруктураРасшифровки = Новый Структура;
				СтруктураРасшифровки.Вставить("Касса", ВыборкаСтатья.Касса);
				СтруктураРасшифровки.Вставить("Статья", ВыборкаСтатья.Статья);
				
				ОбластьСтатья.Параметры.Заполнить(ВыборкаСтатья);
				ОбластьСтатья.Параметры.Расшифровка = СтруктураРасшифровки;
				ТабДок.Вывести(ОбластьСтатья, ВыборкаСтатья.Уровень());
				
				Если Подробно Тогда
					ВыборкаРегистратор = ВыборкаСтатья.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаРегистратор.Следующий() Цикл
						Если ВыборкаРегистратор.Регистратор <> Неопределено И ВыборкаРегистратор.Регистратор.Дата >= НачалоДня(Период.ДатаНачала) И ВыборкаРегистратор.Регистратор.Дата <= КонецДня(Период.ДатаОкончания) Тогда
							ОбластьРегистратор.Параметры.Заполнить(ВыборкаРегистратор);
							ТабДок.Вывести(ОбластьРегистратор, ВыборкаРегистратор.Уровень());
							
							ВыборкаКомментарий = ВыборкаРегистратор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаКомментарий.Следующий() Цикл
								Если ЗначениеЗаполнено(ВыборкаКомментарий.Примечание) И (ЗначениеЗаполнено(ВыборкаКомментарий.СуммаПриход) ИЛИ ЗначениеЗаполнено(ВыборкаКомментарий.СуммаРасход)) Тогда
									ОбластьКомментарий.Параметры.Заполнить(ВыборкаКомментарий);
									ТабДок.Вывести(ОбластьКомментарий, ВыборкаКомментарий.Уровень());
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
	// Показать
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
КонецПроцедуры