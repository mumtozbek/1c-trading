
Процедура Сформировать(ТабДок) Экспорт
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Очистить();
	
	Данные = Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("Агент");
	Данные.Колонки.Добавить("Контрагент");
	
	Макет = ПолучитьМакет("Основной");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	ОбластьШапкаРеквизиты = Макет.ПолучитьОбласть("Шапка|Реквизиты");
	ОбластьШапкаТовар = Макет.ПолучитьОбласть("Шапка|Товар");
	
	ОбластьСтрокаРеквизиты = Макет.ПолучитьОбласть("Строка|Реквизиты");
	ОбластьСтрокаТовар = Макет.ПолучитьОбласть("Строка|Товар");
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ПродажиОбороты.Агент КАК Агент,
		|	ПродажиОбороты.Контрагент КАК Контрагент,
		|	" + ?(Группировка, "ПродажиОбороты.Товар.Бренд КАК Товар", "ПродажиОбороты.Товар КАК Товар") + ",
		|	ПродажиОбороты.КоличествоОборот КАК Количество
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, Период) КАК ПродажиОбороты
		|ИТОГИ
		|	СУММА(Количество)
		|ПО
		|	Агент,
		|	Контрагент,
		|	Товар
		|"
	);
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период.ДатаОкончания));

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаАгент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаАгент.Следующий() Цикл
		ВыборкаКонтрагент = ВыборкаАгент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагент.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаКонтрагент.Контрагент) Тогда
				НоваяСтрока = Данные.Добавить();
				НоваяСтрока.Агент = ВыборкаКонтрагент.Агент;
				НоваяСтрока.Контрагент = ВыборкаКонтрагент.Контрагент;
				
				// Вывод строку для товаров
				ВыборкаТовар = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаТовар.Следующий() Цикл
					НаименованиеКолонки = СокрЛП(ВыборкаТовар.Товар.УникальныйИдентификатор());
					НаименованиеКолонки = "К" + СтрЗаменить(НаименованиеКолонки, "-", "_");
					
					Колонка = Данные.Колонки.Найти(НаименованиеКолонки);
					
					Если Колонка = Неопределено Тогда
	                    Данные.Колонки.Добавить(НаименованиеКолонки,, ВыборкаТовар.Товар.Наименование);
					КонецЕсли;
					
					НоваяСтрока[НаименованиеКолонки] = ВыборкаТовар.Количество;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Вывод заголовка
	ОбластьЗаголовок.Параметры.Период = ПредставлениеПериода(НачалоДня(Период.ДатаНачала), КонецДня(Период.ДатаОкончания));
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// Вывод шапку для реквизитов
	ТабДок.Вывести(ОбластьШапкаРеквизиты);
	
	// Вывод шапку для товаров
	Для Каждого Колонка Из Данные.Колонки Цикл
		Если Колонка.Имя <> "Агент" И Колонка.Имя <> "Контрагент" Тогда
			ОбластьШапкаТовар.Параметры.Товар = Колонка.Заголовок;
			ТабДок.Присоединить(ОбластьШапкаТовар);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из Данные Цикл
		ОбластьСтрокаРеквизиты.Параметры.Заполнить(Строка);
		ОбластьСтрокаРеквизиты.Параметры.Код = Строка.Контрагент.Код;
		ТабДок.Вывести(ОбластьСтрокаРеквизиты);
		
		// Вывод строку для товаров
		Для Каждого Колонка Из Данные.Колонки Цикл
			Если Колонка.Имя <> "Агент" И Колонка.Имя <> "Контрагент" Тогда
				ОбластьСтрокаТовар.Параметры.Количество = Строка[Колонка.Имя];
				ТабДок.Присоединить(ОбластьСтрокаТовар);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	// Показать
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры
