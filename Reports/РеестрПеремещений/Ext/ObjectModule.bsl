
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Основной");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтог = Макет.ПолучитьОбласть("Итог");
	
	ТабДок.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Ссылка КАК Документ,
	|	Ссылка.Склад КАК Склад,
	|	Ссылка.СкладПолучатель КАК Контрагент,
	|	СУММА(Количество) КАК Количество,
	|	Ссылка.Сумма КАК Сумма
	|ИЗ
	|	Документ.ПеремещениеТМЦ.Товары
	|ГДЕ 
	|	Ссылка.Проведен = Истина
	|	И Ссылка.Дата МЕЖДУ &Дата1 И &Дата2");
	
	Если НЕ Склад.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Ссылка.Склад В ИЕРАРХИИ (&Склад)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + " СГРУППИРОВАТЬ ПО
	|	Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка.Дата
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	Документ";
	
	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтог.Следующий();
	
	НомерСтроки = 1;
	ВыборкаДокумент = ВыборкаИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокумент.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаДокумент);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ТабДок.Вывести(ОбластьСтрока);
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	// Итог
	ОбластьИтог.Параметры.Заполнить(ВыборкаИтог);
	ОбластьИтог.Параметры.Сумма = ВыборкаИтог.Сумма;
	ТабДок.Вывести(ОбластьИтог);
	
	// Показать
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры