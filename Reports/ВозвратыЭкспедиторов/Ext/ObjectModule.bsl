
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();
	
	Макет = ПолучитьМакет("Основной");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьОбщийИтог = Макет.ПолучитьОбласть("ОбщиеИтоги");
	ОбластьСотрудник = Макет.ПолучитьОбласть("Сотрудник");
	ОбластьГруппа = Макет.ПолучитьОбласть("Группа");
	ОбластьТовар = Макет.ПолучитьОбласть("Товар");
	ОбластьРегистратор = Макет.ПолучитьОбласть("Регистратор");
	
	ОбластьЗаголовок.Параметры.Период = ПредставлениеПериода(НачалоДня(Период.ДатаНачала), КонецДня(Период.ДатаОкончания));
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВозвратыОбороты.Сотрудник КАК Сотрудник,
	|	ВозвратыОбороты.Товар КАК Товар,
	|	ВозвратыОбороты.Регистратор КАК Регистратор,
	|	СУММА(ВозвратыОбороты.КоличествоОборот) КАК Количество
	|ИЗ
	|	РегистрНакопления.Возвраты.Обороты(&Дата1, &Дата2, Регистратор) КАК ВозвратыОбороты
	|ГДЕ
	|	ИСТИНА");
	
	Если НЕ Сотрудник.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Сотрудник = &Сотрудник";
	КонецЕсли;
	
	Если НЕ Товар.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И (Товар = &Товар ИЛИ Товар В ИЕРАРХИИ (&Товар))";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|СГРУППИРОВАТЬ ПО
	|	Сотрудник,
	|	Товар,
	|	Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Товар.Наименование,
	|	Регистратор.Дата
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	ОБЩИЕ,
	|	Сотрудник,
	|	Товар" + ?(ПоГруппам, " ИЕРАРХИЯ", "") + ",
	|	Регистратор";

	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Товар", Товар);

	РезультатЗапроса = Запрос.Выполнить();

	ТабДок.НачатьАвтогруппировкуСтрок();

	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    Пока ВыборкаОбщийИтог.Следующий() Цикл
		ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(ОбластьОбщийИтог, ВыборкаОбщийИтог.Уровень());

		ВыборкаСотрудник = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСотрудник.Следующий() Цикл
			ОбластьСотрудник.Параметры.Заполнить(ВыборкаСотрудник);
			ТабДок.Вывести(ОбластьСотрудник, ВыборкаСотрудник.Уровень());

			Если ПоТоварам Тогда
				ВыборкаТовар = ВыборкаСотрудник.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаТовар.Следующий() Цикл
					Если ВыборкаТовар.Товар.ЭтоГруппа Тогда
						ОбластьГруппа.Параметры.Заполнить(ВыборкаТовар);
						ТабДок.Вывести(ОбластьГруппа, ВыборкаТовар.Уровень());
					Иначе
						ОбластьТовар.Параметры.Заполнить(ВыборкаТовар);
						ТабДок.Вывести(ОбластьТовар, ВыборкаТовар.Уровень());
					КонецЕсли;
					
					Если Подробно Тогда
						ВыборкаРегистратор = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаРегистратор.Следующий() Цикл
							Если ВыборкаРегистратор.Регистратор <> Неопределено И ВыборкаРегистратор.Регистратор.Дата >= НачалоДня(Период.ДатаНачала) И ВыборкаРегистратор.Регистратор.Дата <= КонецДня(Период.ДатаОкончания) Тогда
								ОбластьРегистратор.Параметры.Заполнить(ВыборкаРегистратор);
								ТабДок.Вывести(ОбластьРегистратор, ВыборкаРегистратор.Уровень());
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ТабДок.ЗакончитьАвтогруппировкуСтрок();

	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры