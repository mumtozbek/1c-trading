
Процедура Сформировать(ТабДок) Экспорт
	ТабДок.Очистить();

	// Подготовка макета
	Макет = ПолучитьМакет("Основной");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьГруппа = Макет.ПолучитьОбласть("Группа");
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	// ОбластьШапка
	ОбластьШапка.Параметры.НазваниеОрганизации = ПараметрыСеанса.Пользователь.Организация;
	ОбластьШапка.Параметры.Номер = Номер;
	ОбластьШапка.Параметры.МОЛ = СокрЛП(Склад.МОЛ);
	ОбластьШапка.Параметры.Период = ПредставлениеПериода(НачалоДня(Период.ДатаНачала), КонецДня(Период.ДатаОкончания));
	ТабДок.Вывести(ОбластьШапка);
	
	// Начальные остатки
	ВыборкаИтогНаНачало = ПолучитьИтоги(НачалоДня(Период.ДатаНачала), Склад);
	
	ОбластьИтого.Параметры.Надпись = "Остаток на " + Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy");
	ОбластьИтого.Параметры.СуммаТовара = ВыборкаИтогНаНачало.Сумма;
	ОбластьИтого.Параметры.Сумма = ВыборкаИтогНаНачало.Сумма;
	ТабДок.Вывести(ОбластьИтого);

	// Создание запроса
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата1", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Ссылка.Номер КАК Номер,
	|	Ссылка.Дата КАК Дата,
	|	Ссылка.Контрагент.ПолноеНаименование КАК Контрагент,
	|	Ссылка КАК Документ,
	|	СуммаБазовая КАК СуммаТовара,
	|	СуммаПродажная КАК Сумма
	|ИЗ
	|	Документ.ПоступлениеТМЦ.Товары
	|ГДЕ
	|	Ссылка.Проведен = Истина
	|	И Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|	И Ссылка.Склад.НеВестиУчет = Ложь";
	
	Если НЕ Склад.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Ссылка.Склад В ИЕРАРХИИ (&Склад)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|	ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ
	|	Ссылка.Номер КАК Номер,
	|	Ссылка.Дата КАК Дата,
	|	Ссылка.Склад КАК Контрагент,
	|	Ссылка КАК Документ,
	|	СуммаБазовая КАК СуммаТовара,
	|	СуммаОтпускная КАК Сумма
	|ИЗ
	|	Документ.ПеремещениеТМЦ.Товары
	|ГДЕ
	|	Ссылка.Проведен = Истина
	|	И Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|	И Ссылка.СкладПолучатель.НеВестиУчет = Ложь";
	
	Если НЕ Склад.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Ссылка.СкладПолучатель В ИЕРАРХИИ (&Склад)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер
	|ИТОГИ
	|	СУММА(СуммаТовара),
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	Документ";
	
	Результат = Запрос.Выполнить();
	
	// Приходы
	ОбластьГруппа.Параметры.Надпись = "ПРИХОД";
	ТабДок.Вывести(ОбластьГруппа);
	
	ВыборкаИтогПриход = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	Пока ВыборкаИтогПриход.Следующий() Цикл
		ВыборкаДокумент = ВыборкаИтогПриход.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		Пока ВыборкаДокумент.Следующий() Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаДокумент);
			ОбластьСтрока.Параметры.Надпись = ВыборкаДокумент.Контрагент;
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
	КонецЦикла;
	
	ВыборкаИтогПриход = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	ВыборкаИтогПриход.Следующий();
	
	// Итого по дебету
	ОбластьИтого.Параметры.Заполнить(ВыборкаИтогПриход);
	ОбластьИтого.Параметры.Надпись = "Итого Приход";
	ОбластьИтого.Параметры.СуммаТовара = ВыборкаИтогПриход.СуммаТовара;
	ОбластьИтого.Параметры.Сумма = ВыборкаИтогПриход.Сумма;
	ТабДок.Вывести(ОбластьИтого);
	
	//// Промежуточные остатки
	//ОбластьСтрока.Параметры.Надпись = "Остатки с учетом приходов:";
	//ОбластьСтрока.Параметры.СуммаТовара = ВыборкаИтогПриход.Сумма + ?(ВыборкаИтогПриход.СуммаТовара = Неопределено, 0, ВыборкаИтогПриход.СуммаТовара);
	//ОбластьСтрока.Параметры.Сумма = ВыборкаИтогПриход.Сумма + ?(ВыборкаИтогПриход.СуммаТовара = Неопределено, 0, ВыборкаИтогПриход.СуммаТовара);
	//ТабДок.Вывести(ОбластьСтрока);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Ссылка.Номер КАК Номер,
	|	Ссылка.Дата КАК Дата,
	|	Ссылка.Контрагент.ПолноеНаименование КАК Контрагент,
	|	Ссылка КАК Документ,
	|	СуммаБазовая КАК СуммаТовара,
	|	0 КАК СуммаТовараМагазин,
	|	СуммаОтпускная КАК Сумма
	|ИЗ
	|	Документ.РеализацияТМЦ.Товары
	|ГДЕ
	|	Ссылка.Проведен = Истина
	|	И Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|	И Ссылка.ВидРеализации.ТипВзаиморасчета = ЗНАЧЕНИЕ(Перечисление.ТипыВзаиморасчета.Безнал)
	|	И Ссылка.Склад.НеВестиУчет = Ложь";
	
	Если НЕ Склад.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Ссылка.Склад В ИЕРАРХИИ (&Склад)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|	ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ
	|	Ссылка.Номер КАК Номер,
	|	Ссылка.Дата КАК Дата,
	|	Ссылка.СкладПолучатель КАК Контрагент,
	|	Ссылка КАК Документ,
	|	СуммаБазовая КАК СуммаТовара,
	|	0 КАК СуммаТовараМагазин,
	|	СуммаОтпускная КАК Сумма
	|ИЗ
	|	Документ.ПеремещениеТМЦ.Товары
	|ГДЕ
	|	Ссылка.Проведен = Истина
	|	И Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|	И Ссылка.Склад.НеВестиУчет = Ложь";
	
	Если НЕ Склад.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + " И Ссылка.Склад В ИЕРАРХИИ (&Склад)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер
	|ИТОГИ
	|	СУММА(СуммаТовара),
	|	СУММА(СуммаТовараМагазин),
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	Документ";
	
	Результат = Запрос.Выполнить();
	
	// Расходы
	ОбластьГруппа.Параметры.Надпись = "РАСХОД";
	ТабДок.Вывести(ОбластьГруппа);
	
	ВыборкаИтогРасход = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	Пока ВыборкаИтогРасход.Следующий() Цикл
		ВыборкаДокумент = ВыборкаИтогРасход.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		Пока ВыборкаДокумент.Следующий() Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаДокумент);
			ОбластьСтрока.Параметры.Документ = ВыборкаДокумент.Документ;
			ОбластьСтрока.Параметры.Надпись = ВыборкаДокумент.Контрагент;
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
	КонецЦикла;
	
	ВыборкаИтогРасход = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	ВыборкаИтогРасход.Следующий();
	
	// Итого по кредиту
	ОбластьИтого.Параметры.Заполнить(ВыборкаИтогРасход);
	ОбластьИтого.Параметры.Надпись = "Итого Расход";
	ОбластьИтого.Параметры.СуммаТовара = ВыборкаИтогРасход.СуммаТовара;
	ОбластьИтого.Параметры.Сумма = ВыборкаИтогРасход.Сумма;
	ТабДок.Вывести(ОбластьИтого);
	
	// Конечные остатки
	ВыборкаИтогНаКонец = ПолучитьИтоги(КонецДня(Период.ДатаОкончания), Склад);
	
	ОбластьИтого.Параметры.Надпись = "Остаток на " + Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy");
	ОбластьИтого.Параметры.СуммаТовара = ПолучитьЧисло(ВыборкаИтогНаНачало.Сумма) + ПолучитьЧисло(ВыборкаИтогПриход.Сумма) - ПолучитьЧисло(ВыборкаИтогРасход.Сумма);
	ОбластьИтого.Параметры.Сумма = ПолучитьЧисло(ВыборкаИтогНаНачало.Сумма) + ПолучитьЧисло(ВыборкаИтогПриход.Сумма) - ПолучитьЧисло(ВыборкаИтогРасход.Сумма);
	ТабДок.Вывести(ОбластьИтого);
	
	// Подвал
	ТабДок.Вывести(ОбластьПодвал);

	// Показать
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры

Функция ПолучитьИтоги(Дата, Склад) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	РегОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.Остатки.Остатки(&Дата, Порожняя = Ложь И Бронь = Ложь) КАК РегОстатки
	|ГДЕ
	|	ИСТИНА
	|	И РегОстатки.Товар.ВидТовара.ТипТовара = ЗНАЧЕНИЕ(Перечисление.ТипыТовара.Товар)
	|	И РегОстатки.Склад.НеВестиУчет = Ложь");
	
	Если ЗначениеЗаполнено(Склад) Тогда
		Запрос.Текст = Запрос.Текст + " И РегОстатки.Склад В ИЕРАРХИИ (&Склад)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИтог = РезультатЗапроса.Выбрать();
	ВыборкаИтог.Следующий();
	
	Возврат ВыборкаИтог;
КонецФункции

Функция ПолучитьЧисло(Значение) Экспорт
	Возврат ?(Значение = Неопределено, 0, Значение);
КонецФункции
